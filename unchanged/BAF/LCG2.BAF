IF 
	Global("LCA_G2_Intro", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_G2_Intro", "GLOBAL", 2)
		ActionOverride(Player1, StartDialog("PLAYER1", Player1))
END

IF
	Global("LCA_EndG2", "GLOBAL", 3)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("LCA_EndG2", "GLOBAL", 4)
		SetGlobal("LCA_CompletedG2", "GLOBAL", 1)
		DisplayStringHead(Player1, ~(The world swirls, and your vision darkens...)~)
		Wait(3)
		SetGlobal("LCA_DemonSpeak", "GLOBAL", 1)
		SetInterrupt(TRUE)
		StartCutSceneMode()
		StartCutScene("LCSENT")
END

IF
	Dead("LCSAREVO")
	Dead("LCSEMAJ")
	Dead("LCANGELO")
	Dead("LCZHALIM")
	Dead("LCGARDUS")
	Dead("LCAASIM")
	Dead("LCDIYAB")
	GlobalLT("LCA_EndG2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_EndG2", "GLOBAL", 1)
		//DisplayStringHead(Myself, ~LCA_EndG2 is 1~)
		Continue()
END

IF
	Dead("LCSAREVO")
	!Global("LCA_G2TimerSet", "LOCALS", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_G2TimerSet", "LOCALS", 1)
		SetGlobalTimer("LCA_SarevokG2", "GLOBAL", THREE_ROUNDS)
END

IF 
	GlobalTimerExpired("LCA_SarevokG2", "GLOBAL")
	GlobalLT("LCA_EndG2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_EndG2", "GLOBAL", 1)
END

IF
	Global("LCA_EndG2", "GLOBAL", 1)
	Dead("CORCUT3")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("LCA_EndG2", "GLOBAL", 2)
		SetGlobal("LCA_CompletedG2", "GLOBAL", 1)
		DisplayStringHead(Player1, ~(The world swirls, and your vision darkens...)~)
		Wait(3)
		SetGlobal("LCA_DemonSpeak", "GLOBAL", 1)
		SetInterrupt(TRUE)
		StartCutSceneMode()
		StartCutScene("LCSENT")
END

IF
	Global("LCA_G2_Intro", "GLOBAL", 3)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("LCA_G2_Intro", "GLOBAL", 4)
		StartCutSceneMode()
		StartCutScene("LCCSG2A")
END
