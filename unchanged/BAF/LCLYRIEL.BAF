IF
	AreaCheck("BG0706")
	Global("LCA_SavedLyriel", "GLOBAL", 2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("LCA_SavedLyriel", "GLOBAL", 3)
		StartDialog("LCLYRIEL", Player1)
END
IF
	AreaCheck("LCDEMSAN")
	Global("LCA_DemonDead", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_DemonDead", "GLOBAL", 2)
		StartDialog("LCLYRIEL", Player1)
END

IF
	Global("LCA_DemonDead", "GLOBAL", 4)
THEN
	RESPONSE #100
		StartDialog("LCLYRIEL", Player1)
END

IF
	AreaCheck("LCPAINT1")
	GlobalLT("LCA_Lyriel", "GLOBAL", 1)
	!Global("LCA_Sing", "LOCALS", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_Sing", "LOCALS", 1)
		DisplayStringHead(Myself, ~(Soft, melancholy singing.)~)
		SetGlobalTimer("LCA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
		Continue()
END

IF
	AreaCheck("LCPAINT1")
	GlobalLT("LCA_Lyriel", "GLOBAL", 1)
	Range([ANYONE], 8)
THEN
	RESPONSE #100
		FaceObject(Player1)
		SmallWait(1)
		StartDialog("LCLYRIEL", Player1)
END

IF
	AreaCheck("LCPAINT1")
	GlobalTimerExpired("LCA_LyrielSingTimer", "GLOBAL")
	!Global("LCA_LeavePrison", "GLOBAL", 1)
THEN
	RESPONSE #50
		DisplayStringHead(Myself, ~(Soft, melancholy singing.)~)
		SetGlobalTimer("LCA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
	RESPONSE #50
		SetGlobalTimer("LCA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
END