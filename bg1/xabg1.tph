PRINT @1000  /* ~Processing BG1 changes...~ */

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg1/baf~

//{ Modifications to Existing Areas

	
	//{ Corwin's House (SE Baldur's Gate)
		ACTION_IF (isEET = 1) THEN BEGIN
			OUTER_SPRINT filename ~BG1316.ARE~
		END ELSE BEGIN
			OUTER_SPRINT filename ~AR1316.ARE~
		END
		COPY_EXISTING ~%filename%~ ~override~
		LPF fj_are_structure
			INT_VAR
			fj_type 		= 2
			fj_box_left		= 533
			fj_box_top		= 108
			fj_box_right	= 580
			fj_box_bottom	= 205
			fj_cursor_idx	= 22
			fj_alt_x		= 550
			fj_alt_y		= 150
			fj_flags		= 1024
			fj_vertex_0     = 533 + (156 << 16)
			fj_vertex_1     = 580 + (205 << 16)
			fj_vertex_2     = 580 + (158 << 16)
			fj_vertex_3     = 533 + (108 << 16)
			STR_VAR
			fj_structure_type = region
			fj_name = ~XA1316FP~
			fj_reg_script = ~XA1316FP~
		END
	//}
	
	//{ Corwin's House (SE Baldur's Gate), Second Floor
		ACTION_IF (isEET = 1) THEN BEGIN
			OUTER_SPRINT filename ~BG1317.ARE~
		END ELSE BEGIN
			OUTER_SPRINT filename ~AR1317.ARE~
		END
		COPY_EXISTING ~%filename%~ ~override~
		LPF ALTER_AREA_ACTOR
			STR_VAR
			actor_name = ~Sleeping Man~
			script_override = ~XA1317DS~
		END
		LPF ALTER_AREA_ACTOR
			STR_VAR
			actor_name = ~Sleeping Woman~
			script_override = ~XA1317DS~
		END
	//}

//}


//{ Script Modifications
	ACTION_IF (isEET = 1) THEN BEGIN
		EXTEND_TOP ~BG1317.BCS~ ~%MOD_FOLDER%/bg1/baf/xabg1317.baf~
	END ELSE BEGIN
		EXTEND_TOP ~AR1317.BCS~ ~%MOD_FOLDER%/bg1/baf/xabg1317.baf~
	END
	
//}

//{ New Items
	//{ Ammo Belt
		COPY ~%MOD_FOLDER%/bg1/itm/xaambelt.itm~ ~override~
		SAY NAME1 @10 /* ~Ammo Belt~*/
		SAY NAME2 @10 /* */
		SAY UNIDENTIFIED_DESC @11 /* */
		SAY DESC @11 /* */
	//}
	
	//{ Flaming Fist Manual
		COPY ~%MOD_FOLDER%/bg1/itm/xaffbook.itm~ ~override~
		SAY NAME1 @12 /* ~Officers' Training Manual~*/
		SAY NAME2 @12 /* */
		SAY UNIDENTIFIED_DESC @13
		SAY DESC @13
	//}
	
	//{ Burned Letter
		COPY ~%MOD_FOLDER%/bg1/itm/xaltrbrn.itm~ ~override~
		SAY NAME1 @16 /* ~Burned Document~ */
		SAY NAME2 @16 /* */
		SAY UNIDENTIFIED_DESC @17
		SAY DESC @17
	//}
//}
