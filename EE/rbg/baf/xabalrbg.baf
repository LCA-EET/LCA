/*
//{ Party Members Leave Until Start of ToB
	IF
		Global("XA_LC_DebriefTriggered", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_DebriefTriggered", "GLOBAL", 2)
			FadeToColor([30.0],0)
			SmallWait(5)
			HideGUI()
			Wait(1)
			ActionOverride(Player2, ChangeAIScript("xapenpm", DEFAULT))
			ActionOverride(Player3, ChangeAIScript("xapenpm", DEFAULT))
			ActionOverride(Player4, ChangeAIScript("xapenpm", DEFAULT))
			ActionOverride(Player5, ChangeAIScript("xapenpm", DEFAULT))
			ActionOverride(Player6, ChangeAIScript("xapenpm", DEFAULT))
			Wait(2)
			StartCutSceneMode()
			StartCutScene("XADEBREF")
	END
//}
*/

//{ 1.4.1e Fixes
IF
	OR(4)
		Global("XA_LC_StayingAtThreeOldKegs","GLOBAL",1)
		Global("XA_LC_StayingAtBladeAndStars","GLOBAL",1)
		Global("XA_LC_StayingAtElfsongTavern","GLOBAL",1)
		Global("XA_LC_StayingAtBlushingMermaid","GLOBAL",1)
	GlobalLT("XA_LC_CorwinSex_3", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CorwinSex_3", "GLOBAL", 1)
		Continue()
END

IF
	GlobalGT("XA_LC_SavedElfsong", "GLOBAL", 0)
	GlobalGT("XA_LC_PrisonKnowledge", "GLOBAL", 0)
	GlobalLT("XA_LC_PrisonKnowledge", "GLOBAL", 3)
	!AreaCheck("XAG1")
	!AreaCheck("XAG2")
	!AreaCheck("XAG3")
	!AreaCheck("XADEMSAN")
	!AreaCheck("XAPAINT1")
THEN
	RESPONSE #100
		SetGlobal("XA_LC_PrisonKnowledge", "GLOBAL", 3)
		Continue()
END
//}

//{ Check for possession of Seldarine Amulet
IF
	PartyHasItem("AMUL27")
	GlobalLT("XA_LC_PossessedSeldarineAmulet", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_PossessedSeldarineAmulet", "GLOBAL", 1)
		Continue()
END
//}

//{ Return to Baldur's Gate Timers
	IF
		Global("XA_LC_ReturnToBG", "GLOBAL", 1)
		GlobalLT("XA_LC_ExitPrison", "GLOBAL", 1)
		GlobalLT("XA_LC_HaltRBGTimers", "GLOBAL", 1)
		OR(7)
			AreaCheck("XAA210") // Elfsong L2 ar0706
			AreaCheck("XAPAINT1")
			AreaCheck("XAG1")
			AreaCheck("XAG2")
			AreaCheck("XAG3")
			AreaCheck("XADEMSAN")
			AreaCheck("XACSHOW")
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_HaltRBGTimers", "GLOBAL", 1)
			Continue()
			//DisplayStringHead(Player1, @4)
	END
	
	IF
		Global("XA_LC_ReturnToBG", "GLOBAL", 1)
		Global("XA_LC_HaltRBGTimers", "GLOBAL", 1)
		!AreaCheck("XAPAINT1")
		!AreaCheck("XAG1")
		!AreaCheck("XAG2")
		!AreaCheck("XAG3")
		!AreaCheck("XADEMSAN")
		!AreaCheck("XACSHOW")
		OR(2)
			!AreaCheck("XAA210") // Elfsong L2 ar0706
			GlobalGT("XA_LC_ExitPrison", "GLOBAL", 0)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_HaltRBGTimers", "GLOBAL", 0)
			Continue()
			//DisplayStringHead(Player1, @5)
	END
	
	IF 
		GlobalTimerExpired("XA_LC_Advance7Timer", "GLOBAL")
		Global("XA_LC_Advance7", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_Advance7", "GLOBAL", 0)
			//DisplayStringHead(Player1, @6)
			Continue()
	END


	//{ Leave for Family Timer
	
		//{ Set the Timer
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinLeaveFamilyTimerSet", "GLOBAL", 1)
			Global("XA_LC_CorwinMetFamily", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobalTimer("XA_LC_CorwinLeaveForFamilyTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_LC_CorwinLeaveFamilyTimerSet", "GLOBAL", 1)
				//DisplayStringHead(Player1, @7)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
		//{ Advance the Timer by One Hour
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			GlobalTimerExpired("XA_LC_CorwinLeaveForFamilyTimer", "GLOBAL")
			GlobalLT("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LC_CorwinLeaveForFamily_Hours", "GLOBAL", 1) // TICK
				SetGlobal("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
				SetInterrupt(TRUE)
				// Don't continue on the tick.
				//DisplayStringHead(Player1, @8)
		END
		//}
		
		//{ Have at least Four Hours Elapsed?
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalGT("XA_LC_CorwinLeaveForFamily_Hours", "GLOBAL", 3)
			GlobalLT("XA_LC_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 2)
				SetGlobal("XA_LC_CorwinLeaveForFamily", "GLOBAL", 1)
				//DisplayStringHead(Player1, @9)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
		//{ If not, then reset the timer.
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinLeaveForFamily_Hours", "GLOBAL", 4)
			GlobalLT("XA_LC_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
			//If the timer has ticked, but has not reached the maximum alloted time, reset the timer.
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_LC_CorwinLeaveForFamilyTicked", "GLOBAL", 0)
				SetGlobalTimer("XA_LC_CorwinLeaveForFamilyTimer", "GLOBAL", ONE_HOUR)
				//DisplayStringHead(Player1, @10)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
	//}	
	
	//{ See Family Timer
	
		//{ Set the Timer
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinFamilyTimerSet", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobalTimer("XA_LC_CorwinFamilyTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_LC_CorwinFamilyTimerSet", "GLOBAL", 1)
				//DisplayStringHead(Player1, @11)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
		
		//{ Advance the Timer by One Hour
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_CorwinFamilyTimerSet", "GLOBAL", 1)
			GlobalTimerExpired("XA_LC_CorwinFamilyTimer", "GLOBAL")
			GlobalLT("XA_LC_CorwinFamilyTimerTicked", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinMetFamily", "GLOBAL", 1)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinNeedSeeFamily", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_LC_CorwinFamilyTimerTicked", "GLOBAL", 1)
				IncrementGlobal("XA_LC_CorwinFamilyTimer_Hours", "GLOBAL", 1)
				SetInterrupt(TRUE)
				//Don't continue on the tick
				//DisplayStringHead(Player1, @12)
		END
		//}
		
		//{ Corwin Complain about not seeing family.
		IF
			InParty("XACORWIN")
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_CorwinFamilyTimerTicked", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinMetFamily", "GLOBAL", 1)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			GlobalLT("XA_LC_CorwinNeedSeeFamily", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
			!AreaCheck("XACSHOW")			
			OR(2)
				GlobalLT("XA_LC_SavedTimotey", "GLOBAL", 1)
				GlobalGT("XA_LC_SavedTimotey", "GLOBAL", 4)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_CorwinFamilyTimerTicked", "GLOBAL", 2)
				SetGlobal("XA_LC_CorwinNeedSeeFamily", "GLOBAL", 1)
				//DisplayStringHead(Player1, @13)
		END
		//}
	//}
	
	//{ Duke Debriefed Timer
	
		//{ Set the Timer
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_LC_DukeDebriefTimerSet", "GLOBAL", 1)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_LC_DukeDebriefTimerSet", "GLOBAL", 1)
				SetGlobalTimer("XA_LC_DukeDebriefTimer", "GLOBAL", ONE_HOUR)
				//DisplayStringHead(Player1, @14)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
		
		//{ Advance the timer by One Hour
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_LC_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalTimerExpired("XA_LC_DukeDebriefTimer", "GLOBAL")
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_LC_DukeDebriefTimer_Hours", "GLOBAL", 1) // TICK
				SetGlobal("XA_LC_Advance7", "GLOBAL", 0)
				SetGlobal("XA_LC_DukeDebriefTimerTick", "GLOBAL", 1)
				SetInterrupt(TRUE)
				//DisplayStringHead(Player1, @15)
		END
		//}
	
		//{ Have 24 Hours Elapsed?
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalGT("XA_LC_DukeDebriefTimer_Hours", "GLOBAL", 23)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
			OR(2)
				Global("XA_LC_CorwinMetFamily", "GLOBAL", 2)
				Global("XA_LC_CorwinWithFamily", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_DukeDebriefTimerTick", "GLOBAL", 2)
				SetGlobal("XA_LC_DukeDebrief_BenceSpawn", "GLOBAL", 1)
				SetGlobal("XA_LC_DukeDebrief_TimerElapsed", "GLOBAL", 1)
				//DisplayStringHead(Player1, @16)
				Continue()
		END
		//}
	
		//{ Less than 24 Hours Has Elapsed
		IF
			Global("XA_LC_ReturnToBG", "GLOBAL", 1)
			Global("XA_LC_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalLT("XA_LC_DukeDebriefTimer_Hours", "GLOBAL", 24)
			GlobalLT("XA_LC_DukesDebriefed", "GLOBAL", 1)
			Global("XA_LC_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetGlobalTimer("XA_LC_DukeDebriefTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_LC_DukeDebriefTimerTick", "GLOBAL", 0)
				//DisplayStringHead(Player1, @17)
				Continue()
		END
		//}
	//}
//}

//{ v1.0.14a Fix - Mark Beno as Killed
	IF
		Global("XA_LC_CorwinKillBeno", "GLOBAL", 1)
		GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CorwinKillBeno", "GLOBAL", 2)
			Continue()
	END
//}

//{ v1.0.14a Fix - Mark Illasera as Killed
	IF
		Global("XA_LC_IllaseraDead", "GLOBAL", 1)
		GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_IllaseraDead", "GLOBAL", 2)
			Continue()
	END
//}


//{ v1.0.14a Fix - Marriage Proposal Fix
	IF
		Global("XA_LC_CorwinProposalCS", "GLOBAL", 2)
		GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CorwinProposalCS", "GLOBAL", 3)
			Continue()
	END
//}

//{ Post Quest Dialogue - Trouble in the Tower
	IF
		Global("XA_LC_SavedTimotey", "GLOBAL", 5)
		GlobalLT("XA_LC_TroubleTalk", "GLOBAL", 1)
		!InParty("XACORWIN")
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_TroubleTalk", "GLOBAL", 2)
			Continue()
	END
//}

//{ Mark Sex 1, 2 Complete
IF
	GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 0)
	Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
	OR(2)
		Global("XA_LC_CorwinSex_1", "GLOBAL", 1)
		Global("XA_LC_CorwinSex_2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CorwinSex_1", "GLOBAL", 5)
		SetGlobal("XA_LC_CorwinSex_2", "GLOBAL", 5)
		Continue()
END
//}

//{ Mark Sex 3 Complete
IF
	Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
	Global("XA_LC_CorwinSex_3", "GLOBAL", 1)
	OR(2)
		GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 1)
		GlobalGT("XA_LC_DukesDebriefed", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CorwinSex_3", "GLOBAL", 7)
		Continue()
END
//}