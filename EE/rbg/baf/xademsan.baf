IF
	Global("XA_LC_EnterSanctum", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_EnterSanctum", "GLOBAL", 0)
		FadeFromColor([30.0],0)
		Wait(1)
END

//{ Narzugon Dialog Initiation
IF
	Global("XA_LC_ParleyAttack", "GLOBAL", 1)
	InMyArea(Player1)
THEN
	RESPONSE #100
		ActionOverride("XANARZU",StartDialog("XANARZU", Player1))
END

IF
	Global("XA_LC_ConfrontNarzugon", "GLOBAL", 1)
	InMyArea(Player1)
THEN
	RESPONSE #100
		ActionOverride("XANARZU",StartDialog("XANARZU", Player1))
END

IF
	InMyArea(Player1)
	Global("XA_LC_CompletedG1", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CompletedG1", "GLOBAL", 2)
		ActionOverride("XANARZU", StartDialog("XANARZU", Player1))
		SetInterrupt(TRUE)
END

IF
	InMyArea(Player1)
	Global("XA_LC_CompletedG2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CompletedG2", "GLOBAL", 2)
		ActionOverride("XANARZU", StartDialog("XANARZU", Player1))
		SetInterrupt(TRUE)
END

IF
	InMyArea(Player1)
	Global("XA_LC_CompletedG3", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CompletedG3", "GLOBAL", 2)
		ActionOverride("XANARZU", StartDialog("XANARZU", Player1))
		SetInterrupt(TRUE)
END
//}

//{ Kill Narzugon
IF
	HPPercentLT("XANARZU", 10)
	Global("XA_LC_NarzugonHostile", "GLOBAL", 1)
THEN
	RESPONSE #33
		ActionOverride("XANARZU", SetItemFlags("XAQUIVER", 1, TRUE))
		SetGlobal("XA_LC_IncubusQuiver", "GLOBAL", 1)
		Continue()
	RESPONSE #33
		ActionOverride("XANARZU", SetItemFlags("XASWORD", 1, TRUE))
		SetGlobal("XA_LC_IncubusSword", "GLOBAL", 1)
		Continue()
	RESPONSE #33
		ActionOverride("XANARZU", SetItemFlags("XAMSRING", 1, TRUE))
		SetGlobal("XA_LC_IncubusRing", "GLOBAL", 1)
		Continue()
END

IF
	HPPercentLT("XANARZU", 10)
	Global("XA_LC_NarzugonHostile", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_IncubusDefeated", "GLOBAL", 1)
		SetGlobal("XA_LC_NarzugonHostile", "GLOBAL", 2)
		DisplayStringHead("XANARZU", @0)
		Kill("XANARZU")	
END
//}

IF
	Global("XA_LC_DemonDead", "GLOBAL", 3)
THEN
	RESPONSE #100
		ActionOverride("XALYRIEL", SetSequence(SEQ_CAST))
		Wait(2)
		ApplySpell(Player1, CLERIC_RESURRECTION)
		ApplySpell(Player2, CLERIC_RESURRECTION)
		ApplySpell(Player3, CLERIC_RESURRECTION)
		ApplySpell(Player4, CLERIC_RESURRECTION)
		ApplySpell(Player5, CLERIC_RESURRECTION)
		ApplySpell(Player6, CLERIC_RESURRECTION)
		ActionOverride("XALYRIEL", SetSequence(SEQ_READY))
		Wait(2)
		ActionOverride("XALYRIEL", SetSequence(SEQ_CAST))
		Wait(2)
		ApplySpell(Player1, CLERIC_CURE_CRITICAL_WOUNDS)
		ApplySpell(Player2, CLERIC_CURE_CRITICAL_WOUNDS)
		ApplySpell(Player3, CLERIC_CURE_CRITICAL_WOUNDS)
		ApplySpell(Player4, CLERIC_CURE_CRITICAL_WOUNDS)
		ApplySpell(Player5, CLERIC_CURE_CRITICAL_WOUNDS)
		ApplySpell(Player6, CLERIC_CURE_CRITICAL_WOUNDS)
		ActionOverride("XALYRIEL", SetSequence(SEQ_READY))
		SetGlobal("XA_LC_DemonDead", "GLOBAL", 4)
END


IF
	Dead("XANARZU")
	GlobalLT("XA_LC_DemonDead", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_DemonDead", "GLOBAL", 1)
		StartCutSceneMode()
		StartCutScene("XADEMDED")
END

IF
	HPPercentLT("XANARZU", 100)
	GlobalLT("XA_LC_ConfrontNarzugon", "GLOBAL", 1)
THEN
	RESPONSE #100
		ApplySpell("XANARZU", CLERIC_HEAL)
END
