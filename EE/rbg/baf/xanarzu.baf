//{ Apply Buffs When Hostile
IF
	Global("XA_LC_NarzugonHostile", "LOCALS", 1)
	GlobalGT("XA_LC_PrisonKnowledge", "GLOBAL", 2)
	GlobalLT("XA_LC_IncubusBuff", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_IncubusBuff", "LOCALS", 1)
		ClearActions(Myself)
		ReallyForceSpell(Myself, WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself, WIZARD_STONE_SKIN)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XA_LC_NarzugonHostile", "LOCALS", 1)
	GlobalLT("XA_LC_PrisonKnowledge", "GLOBAL", 3)
	GlobalLT("XA_LC_IncubusBuff", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_IncubusBuff", "LOCALS", 1)
		ClearActions(Myself)
		ReallyForceSpell(Myself, WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself, WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself, WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself, WIZARD_SHIELD)
		
		SetInterrupt(TRUE)
		Continue()
END
//}

IF
	HPPercentLT(Myself, 100)
	GlobalLT("XA_LC_ParleyAttack", "GLOBAL", 1)
	InMyArea(Player1)
	GlobalLT("XA_LC_ConfrontNarzugon", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_ParleyAttack", "GLOBAL", 1)
END

//{ Block fight script if not hostile
IF 
	GlobalLT("XA_LC_NarzugonHostile", "LOCALS", 1)
THEN
	RESPONSE #100
		NoAction()
END
//}

//{ fight script

IF
	See(NearestEnemyOfType([0.0.0.0.0.SUMMONED]))
	Range(LastSeenBy(Myself),15)
	LevelLT(LastSeenBy(Myself),10)
	!GlobalTimerNotExpired("XA_LC_IncubusDeath","LOCALS")
	RandomNum(2,1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_IncubusDeath","LOCALS",TWO_ROUNDS)
		ForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)  // SPWI605.SPL (Death Spell)
		SetInterrupt(TRUE)
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
	CheckStat(Myself,0,STONESKINS)
	!GlobalTimerNotExpired("XA_LC_IncubusStone","LOCALS")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_IncubusStone","LOCALS",TWO_MINUTES)
		ForceSpell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
		SetInterrupt(TRUE)
END

IF
	See(NearestEnemyOf(Myself))
	RandomNum(2,1)
	!StateCheck(NearestEnemyOf(Myself),STATE_CHARMED)
	!StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
	!GlobalTimerNotExpired("XA_LC_Gaze","LOCALS")
THEN
	RESPONSE #50
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Gaze","LOCALS",TWO_ROUNDS)
		FaceObject(NearestEnemyOf(Myself))
		ReallyForceSpellRES("beguile",NearestEnemyOf(Myself))
		SetInterrupt(TRUE)
	RESPONSE #50
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Gaze","LOCALS",TWO_ROUNDS)
		FaceObject(NearestEnemyOf(Myself))
		ReallyForceSpellRES("insanity",NearestEnemyOf(Myself))
		SetInterrupt(TRUE)		
		
END

IF
	!GlobalTimerNotExpired("XA_LC_Finger","LOCALS")
	RandomNum(3,1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Finger","LOCALS",THREE_MINUTES)
		ForceSpell(NearestEnemyOf(Myself),CLERIC_FINGER_OF_DEATH)
		SetInterrupt(TRUE)	
END

IF
	!GlobalTimerNotExpired("XA_LC_Unholy","LOCALS")
	RandomNum(2,1)
	See(NearestEnemyOf(Myself))
	!Alignment(NearestEnemyOf(Myself),MASK_EVIL)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Unholy","LOCALS",FIVE_ROUNDS)
		ForceSpell(Myself,CLERIC_UNHOLY_WORD)  // SPPR715.SPL (Unholy Word)
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("XA_LC_Quake","LOCALS")
	RandomNum(3,1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Quake","LOCALS",TEN_ROUNDS)
		ForceSpell(Myself,CLERIC_EARTHQUAKE)  // SPPR720.SPL (Earthquake)
		SetInterrupt(TRUE)
END

IF
	GlobalTimerNotExpired("XA_LC_Harm","LOCALS")
	RandomNum(3,1)
	OR(2)
		See(NearestEnemyOf(Myself))
		See(LeastDamagedOf(Myself))
	Range(LastSeenBy(Myself),10)
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_Harm","LOCALS",FOUR_MINUTES)
		ForceSpell(LastSeenBy(Myself),ENEMY_CLERIC_HARM)
		SetInterrupt(TRUE)		
END

IF
	Range(NearestEnemyOf(Myself),4)
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),30)
	RESPONSE #50
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

//}