IF
	Global("XA_LC_SarevokHostile", "GLOBAL", 1)
	GlobalLT("XA_LC_SarevokTransformTimerSet", "MYAREA", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_SarevokTransformTimerSet", "MYAREA", 1)
		SetGlobalTimer("XA_LC_SarevokTransformTimer", "GLOBAL", TEN_ROUNDS)
		Continue()
END

IF
	GlobalTimerExpired("XA_LC_SarevokTransformTimer", "GLOBAL")
	!Dead("xasarevo")
	GlobalLT("XA_LC_SarevokTransform", "MYAREA", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_SarevokTransform", "MYAREA", 1)
		DisplayStringHead("xasarevo", @1)
		Wait(3)
		CreateVisualEffectObject("ICFIRSDI", "xasarevo") 
		Wait(1)
		ActionOverride("xasarevo", ChangeAnimation("xasarev2"))
END

IF 
	Global("XA_LC_G2_Intro", "GLOBAL", 1)
THEN
	RESPONSE #100
		Explore()
		SetGlobal("XA_LC_G2_Intro", "GLOBAL", 2)
		ActionOverride(Player1, StartDialog("PLAYER1", Player1))
END

IF
	Global("XA_LC_EndG2", "GLOBAL", 3)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_EndG2", "GLOBAL", 4)
		SetGlobal("XA_LC_DemonSpeak", "GLOBAL", 1)
		SetGlobal("XA_LC_CompletedG2", "GLOBAL", 1)
		DisplayStringHead(Player1, @0)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XA_LC_CompletedG2", "GLOBAL", 1)
	OR(6)
		InMyArea(Player1)
		InMyArea(Player2)
		InMyArea(Player3)
		InMyArea(Player4)
		InMyArea(Player5)
		InMyArea(Player6)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		StartCutSceneMode()
		StartCutScene("XASENT")
		SetInterrupt(TRUE)
END

IF
	OR(2)
		Dead("XASAREVO")
		Dead("XASAREV2")
	Dead("XASEMAJ")
	Dead("XAANGELO")
	Dead("XAZHALIM")
	Dead("XAGARDUS")
	Dead("XAAASIM")
	Dead("XADIYAB")
	GlobalLT("XA_LC_EndG2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_EndG2", "GLOBAL", 1)
		//DisplayStringHead(Myself, ~XA_EndG2 is 1~)
		Continue()
END

IF
	Dead("XASAREVO")
	GlobalLT("XA_LC_G2TimerSet", "LOCALS", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_G2TimerSet", "LOCALS", 1)
		SetGlobalTimer("XA_LC_SarevokG2", "MYAREA", TWO_ROUNDS)
END

IF 
	GlobalTimerExpired("XA_LC_SarevokG2", "MYAREA")
	GlobalLT("XA_LC_EndG2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_EndG2", "GLOBAL", 1)
END

IF
	Global("XA_LC_EndG2", "GLOBAL", 1)
	Dead("XACC3")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_EndG2", "GLOBAL", 2)
		SetGlobal("XA_LC_CompletedG2", "GLOBAL", 1)
		SetGlobal("XA_LC_DemonSpeak", "GLOBAL", 1)
		SetInterrupt(TRUE)
END

IF
	Global("XA_LC_G2_Intro", "GLOBAL", 3)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_G2_Intro", "GLOBAL", 4)
		StartCutSceneMode()
		StartCutScene("XACSG2A")
END
