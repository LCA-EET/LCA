//Note: Variable Key Max Length Appears to be 32.

//{ 00: Blocking Actions
	IF
		Global("XA_LC_CorwinIrenicus", "GLOBAL", 4)
	THEN
		RESPONSE #100
			////DisplayStringHead(Myself, ~CI4 Script Blocking~)
			NoAction()
	END
//}

//{ 01: Amulet / Letter on Party Join
	
	//{ Include the letter sent by the Player
	IF
		InParty(Myself)
		Global("XA_LC_LetterSent", "GLOBAL", 1)
		GlobalLT("XA_LC_CreatedLetterInventory", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			CreateItem("XALCORW1",0,0,0)
			SetGlobal("XA_LC_CreatedLetterInventory", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END

	IF
		InParty(Myself)
		Global("XA_LC_LetterSent", "GLOBAL", 2)
		GlobalLT("XA_LC_CreatedLetterInventory", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			CreateItem("XALCORW2",0,0,0)
			SetGlobal("XA_LC_CreatedLetterInventory", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	//}

	//{ Corwin Amulet 
	IF
		InParty(Myself)
		PartyHasItem("XAAMULET")	
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			DestroyItem("XAAMULET")
			ActionOverride(Player1, DestroyItem("XAAMULET"))
			ActionOverride(Player2, DestroyItem("XAAMULET"))
			ActionOverride(Player3, DestroyItem("XAAMULET"))
			ActionOverride(Player4, DestroyItem("XAAMULET"))
			ActionOverride(Player5, DestroyItem("XAAMULET"))
			ActionOverride(Player6, DestroyItem("XAAMULET"))
			SetInterrupt(TRUE)
			Continue()
	END
	//}

//}



//{ 19: Corwin Turn Hostile
IF 
	InParty(Myself)
	Global("XA_LC_CorwinHostile", "GLOBAL", 1)
THEN 
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CorwinHostile", "GLOBAL", 2)
		DisplayStringHead(Myself, @0)
		SetLeavePartyDialogFile()
		ChangeAIScript("", DEFAULT)
		LeaveParty()
		Enemy()
		SetInterrupt(TRUE)
END
//}

//{ Check if Corwin is a Healer (needed for second SoA intimate encounter)
IF
	OR(5)
		HaveSpellRES("SPPR103") // Cure Light Wounds
		HaveSpellRES("SPPR315") // Cure Medium Wounds
		HaveSpellRES("SPPR401") // Cure Serious Wounds
		HaveSpellRES("SPPR502") // Cure Critical Wounds
		HaveSpellRES("SPPR514") // Mass Cure
	GlobalLT("XA_LC_CorwinCanHeal", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CorwinCanHeal", "GLOBAL", 1)
		Continue()
END
//}

//{ 1.4.2 Corwin Reminder
IF
	AreaCheck("XACS13")
	GlobalTimerExpired("XA_LC_Reminder", "LOCALS")
	GlobalLT("XA_LC_PartyRemovalProcessing", "GLOBAL", 1)
THEN
	RESPONSE #100
		DisplayStringHead(Myself, @1) /* ~Talk to me when you're ready to leave.~ */
		SetGlobalTimer("XA_LC_Reminder", "LOCALS", THREE_ROUNDS)
		Continue()
END
//}