
//{	Rejoin processing 
	IF 
		!InParty(Myself)
		Global("XA_LC_CaelarRejoinProcessed", "GLOBAL", 1)
	THEN 
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CaelarJoined","LOCALS",1)
			SetGlobal("XA_LC_AskedToRejoin", "LOCALS", 0)
			SetGlobal("XA_LC_ResetBanterTimer", "LOCALS", 1)
			SetGlobal("XA_LC_CaelarRepWarning", "GLOBAL", 0)
			SetGlobal("XA_LC_CaelarRejoinProcessed", "GLOBAL", 2)
			SetInterrupt(TRUE)
			JoinParty()
	END
//}
	
//{ Reputation Handling
IF 
	Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
	ReputationLT(Player1,7)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CaelarLeftPoorRep", "GLOBAL", 1)
		StartDialog("XACAELAJ", Player1)
		SetInterrupt(TRUE)
END

IF
	InParty(Myself)
	ReputationLT(Player1,8)
	GlobalLT("XA_LC_CaelarRepWarning", "GLOBAL", 1)
THEN 
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_CaelarRepWarningTimer", "GLOBAL", ONE_HOUR)
		SetGlobal("XA_LC_CaelarRepWarning", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF 
	Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
	Global("XA_LC_CaelarRepWarning", "GLOBAL", 1)
	ReputationLT(Player1,8)
	GlobalTimerExpired("XA_LC_CaelarRepWarningTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CaelarRepWarning", "GLOBAL", 2)
		StartDialog("XACAELAJ", Player1)
		SetInterrupt(TRUE)
END

IF 
	GlobalGT("XA_LC_CaelarRepWarning", "GLOBAL", 0)
	ReputationGT(Player1,7)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CaelarRepWarning", "GLOBAL", 0)
		Continue()
END
//}

//{ Player Interactions
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_CaelarThanks", "GLOBAL")
		GlobalLT("XA_LC_SummonAun", "GLOBAL", 4)
	THEN
		RESPONSE #100
			SetInterrupt(TRUE)
			SetGlobal("XA_LC_ResetBanterTimer", "LOCALS", 1)
			SetGlobal("XA_LC_SummonAun", "GLOBAL", 4)
			Interact(Player1)
			SetInterrupt(FALSE)
	END

//} End Player Interactions
