//{ LCA Related Changes

	IF
		GlobalLT("XA_LC_EnteredToB", "GLOBAL", 1)
		GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 0)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_EnteredToB", "GLOBAL", 1)
			SetGlobal("XA_LC_ArrivedInUndercity", "GLOBAL", 0)
			ChangeStat(Player1,FATIGUE,1,SET)
			ChangeStat(Player2,FATIGUE,1,SET)
			ChangeStat(Player3,FATIGUE,1,SET)
			ChangeStat(Player4,FATIGUE,1,SET)
			ChangeStat(Player5,FATIGUE,1,SET)
			ChangeStat(Player6,FATIGUE,1,SET)
			// Prevent entry into states associated with AR4000
			SetGlobal("IllaseraSpawn", "GLOBAL", 1)
			SetGlobal("FORCETOBPARTYMOVE4000","GLOBAL",1)
			SetGlobal("SetSolarDreamTimer","AR4000",1)
			SetGlobal("HeadsSave","AR4000",3)
			Continue()
	END
	//{ Replace Journal Entry
		IF
			GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 0)
			Global("TurnDay", "AR4000", 1)
			GlobalLT("XA_LC_ToBStartJournal", "MYAREA", 1)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_ToBStartJournal", "MYAREA", 1)
				EraseJournalEntry(#73256)
				AddJournalEntry(@0, INFO) /*~It has begun. Even before I left for Saradush, the news had already begun pouring in from everywhere... tales of armies on the move, whole towns being destroyed to get at some lone wizard, massive carnage and destruction along the entire Sword Coast. It all links to the Bhaalspawn. They are being hunted, or hunting each other... whatever the case, wherever a child of Bhaal is, violence soon follows. And so the fear and panic have spread, with any innocent even suspected of having the taint being driven from their homes or killed outright. Baldur's gate is not immune, and I sensed considerable relief upon my departure from the very city that I helped save. The caravan has reached its destination on the outskirts of the forest to the northwest of Saradush. While traversing the woods, I've come across this strange grove, filled with ancient stone carvings. There is a strange energy in the air... I will investigate the grove, then continue on toward Saradush.~*/
		END
	//}
	
	//{ Reset Worldmap
		IF
			Global("XA_LC_RBGWorldMap", "GLOBAL", 1)
			GlobalLT("XA_LC_ToBMapSet", "MYAREA", 1)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_ToBMapSet", "MYAREA", 1)
				SetWorldmap("WORLDM25")
				Continue()
		END
	//}
	
	//{ Fire Giant Scouting Party
		IF
			GlobalGT("XA_LC_ReturnToBG", "GLOBAL", 0)
			GlobalLT("XA_LC_GiantsSpawned", "GLOBAL", 1)
			GlobalTimerExpired("XA_LC_GiantSpawnTimer", "GLOBAL")
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_GiantsSpawned", "GLOBAL", 1)
				//760 380
				ScreenShake([20.20],20)
				CreateCreature("XAFGSC1",[800.1600],N)
				CreateCreature("XAFGSC2",[639.1684],N)
				CreateCreature("XAFGSC3",[1040.1772],NW)
		END

		IF
			Global("XA_LC_GiantsSpawned", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_GiantsSpawned","GLOBAL",2)  // Spirit Heads
				Wait(1)
				DisplayStringNoName(Player1,@2)
				ActionOverride("XAFGSC1", MoveToPoint([720.670]))
				ActionOverride("XAFGSC2", MoveToPoint([720.670]))
				ActionOverride("XAFGSC3", MoveToPoint([720.670]))
		END

		IF
			Dead("XAFGSC1")
			Dead("XAFGSC2")
			Dead("XAFGSC3")
			GlobalGT("XA_LC_GiantsSpawned", "GLOBAL", 0)
			GlobalLT("XA_LC_SolarTimer", "MYAREA", 1)
		THEN
			RESPONSE #100
				SetGlobal("XA_LC_SolarTimer", "MYAREA", 1)
				SetGlobalTimer("DreamSolarTimer", "GLOBAL", ONE_MINUTE)
		END
	//}
//} 