IF
	Global("XA_LC_CrusadersGone", "GLOBAL", 1)
	Global("XA_LC_SparedCaelar", "GLOBAL", 1)
	GlobalLT("XA_LC_CrusaderJournal", "MYAREA", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CrusaderJournal", "MYAREA", 1)
		AddJournalEntry(@0, INFO)
		ActionOverride("XACAELA2", StartDialog("XACAELA2", Player1))
END

IF
	Global("XA_LC_CrusadersGone", "GLOBAL", 1)
	Global("XA_LC_KilledCaelar", "GLOBAL", 1)
	GlobalLT("XA_LC_CrusaderJournal", "MYAREA", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CrusaderJournal", "MYAREA", 1)
		AddJournalEntry(@1, INFO)
END

IF
	Global("XA_LC_CaelarRescue", "GLOBAL", 1)
	GlobalLT("XA_LC_CaelarSuicide", "GLOBAL", 1)
	GlobalLT("XA_LC_CaelarSpawn", "MYAREA", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_CaelarSpawn", "MYAREA", 1)
		CreateCreature("XACAELA2", [787.555], S)
		Continue()
END

IF
	Global("XA_LC_CaelarRescue", "GLOBAL", 1)
	Global("XA_LC_CaelarSuicide", "GLOBAL", 2)
THEN
	RESPONSE #100
		ActionOverride("XAASHASP", StartDialog("XAASHASP", Player1))
END

IF
	InMyArea("XACAELA2")
	InMyArea("XAASHASP")
	GlobalLT("XA_LC_AshaCaelar", "GLOBAL", 1)
THEN
	RESPONSE #100
		Wait(1)
		ActionOverride("XACAELA2", StartDialog("XACAELA2", Player1))
END

IF
	InMyArea("XABENOSP")
	Global("XA_LC_ElfSpirit", "GLOBAL", 2)
	!InMyArea("XAELF01")
	!InMyArea("XAELF02")
	!InMyArea("XAELF03")
	IsValidForPartyDialogue("XACORWIN")
	GlobalLT("XA_LC_BenoGoodbye", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_BenoGoodbye", "GLOBAL", 1)
		ActionOverride("XABENOSP", StartDialog("XABENOSP", "XACORWIN"))
END

IF
	InMyArea("XABENOSP")
	Global("XA_LC_ElfSpirit", "GLOBAL", 2)
	!InMyArea("XAELF01")
	!InMyArea("XAELF02")
	!InMyArea("XAELF03")
	!IsValidForPartyDialogue("XACORWIN")
	GlobalLT("XA_LC_BenoGoodbye", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_BenoGoodbye", "GLOBAL", 2)
		Continue()
END

IF
	Dead("XALODOB")
	GlobalLT("XA_LC_LodobDead", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_LC_LodobDead", "GLOBAL", 1)
		SetGlobal("XA_LC_ElfSpirit", "GLOBAL", 1)
		Continue()
END

IF
	Global("XA_LC_ElfSpirit", "GLOBAL", 1)
THEN
	RESPONSE #100
		ActionOverride("XAELF01", StartDialog("XAELF01", Player1))
END

IF
	Global("XA_LC_ElfSpirit", "GLOBAL", 2)
	InMyArea("XAELF01")
THEN
	RESPONSE #100
		ActionOverride("XAELF01", ForceSpell("XAELF01", FLASHY_2))
		Wait(1)
		ActionOverride("XAELF01", DestroySelf())
		Continue()
END

IF
	Global("XA_LC_ElfSpirit", "GLOBAL", 2)
	InMyArea("XAELF02")
THEN
	RESPONSE #100
		ActionOverride("XAELF02", ForceSpell("XAELF02", FLASHY_2))
		Wait(1)
		ActionOverride("XAELF02", DestroySelf())
		Continue()
END

IF
	Global("XA_LC_ElfSpirit", "GLOBAL", 2)
	InMyArea("XAELF03")
THEN
	RESPONSE #100
		ActionOverride("XAELF03", ForceSpell("XAELF03", FLASHY_2))
		Wait(1)
		ActionOverride("XAELF03", DestroySelf())
		Continue()
END

IF
	Global("XA_LC_BenoGoodbye", "GLOBAL", 2)
THEN
	RESPONSE #100
		ActionOverride("XABENOSP", ForceSpell("XABENOSP", FLASHY_2))
		Wait(1)
		ActionOverride("XABENOSP", DestroySelf())
		Continue()
END

