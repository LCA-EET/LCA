//{ Can Talk to Caelar
	IF
		ActionListEmpty()
		See("XACAELAR")
		IsValidForPartyDialogue("XACAELAR")
		!Global("XA_LC_CanTalkToCaelar", "LOCALS", 1)
		Global("XA_LC_CanTalk", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CanTalkToCaelar", "LOCALS", 1)
			Continue()
	END

	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToCaelar", "LOCALS", 1)
		OR(3)	
			!See("XACAELAR")
			!IsValidForPartyDialogue("XACAELAR")
			!Global("XA_LC_CanTalk", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CanTalkToCaelar", "LOCALS", 0)
			Continue()
	END
//}

//{ Can Talk to Sarevok
	IF
		ActionListEmpty()
		See("Sarevok")
		IsValidForPartyDialogue("Sarevok")
		!Global("XA_LC_CanTalkToSarevok", "LOCALS", 1)
		Global("XA_LC_CanTalk", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CanTalkToSarevok", "LOCALS", 1)
			Continue()
	END

	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToSarevok", "LOCALS", 1)
		OR(3)	
			!See("Sarevok")
			!IsValidForPartyDialogue("Sarevok")
			!Global("XA_LC_CanTalk", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CanTalkToSarevok", "LOCALS", 0)
			Continue()
	END
	//}
	
//{ Corwin Complain about Sarevok
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		InParty("Sarevok")
		GlobalTimerExpired("XA_LC_CorwinComplainSarevokTimer", "GLOBAL")
		Global("XA_LC_CorwinComplainSarevok", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CorwinComplainSarevok", "GLOBAL", 2)
			StartDialog("XACORWIJ", Player1)
			Continue()
	END
//}

//{ Saradush Destruction
	IF 
		ActionListEmpty()
		GlobalTimerExpired("XA_LC_YagaTimer", "GLOBAL")
		GlobalLT("XA_LC_SaradushDestroyed", "GLOBAL", 1)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_SaradushDestroyed", "GLOBAL", 1)
			//Dialog J-70
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
//}

//{ Killed Wraith
	IF
		Global("XA_LC_RohmaWraith", "GLOBAL", 1)
		Dead("hgwra01")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_RohmaWraith", "GLOBAL", 2)
			SetGlobalTimer("XA_LC_RohmaWraithTimer", "LOCALS", ONE_ROUND)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		!ActuallyInCombat()
		Global("XA_LC_RohmaWraith", "GLOBAL", 2)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_RohmaWraithTimer", "LOCALS")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_RohmaWraith", "GLOBAL", 3)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}

//{ Saved Father in Saradush
	IF
		AreaCheck("AR5000")
		Global("FatherKilled","AR5000",2)
		GlobalLT("XA_LC_CommentOnFather", "LOCALS", 1)
		!InMyArea("orphan2")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CommentOnFather", "LOCALS", 1)
			DisplayStringHead(Myself, @22)
			SetInterrupt(TRUE)
			Continue()
	END
//}

//{ Entered Fire Temple 5204
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalk", "LOCALS", 1)
		GlobalLT("XA_LC_AR5204TimerSet", "LOCALS", 1)
		AreaCheck("AR5204")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_AR5204TimerSet", "LOCALS", 1)	
			SetGlobalTimer("XA_LC_AR5204Timer", "LOCALS", ONE_ROUND)
			SetInterrupt(TRUE)
			Continue()
	END

	IF 
		ActionListEmpty()
		Global("XA_LC_CanTalk", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_AR5204Timer", "LOCALS")
		GlobalLT("XA_LC_AR5204_Talk", "LOCALS", 1)
		AreaCheck("AR5204")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			DisplayStringHead(Myself, @17)	
			SetGlobal("XA_LC_AR5204_Talk", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END
//}

//{ Entered Sendai's Lair
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalk", "LOCALS", 1)
		GlobalLT("XA_LC_AR6101TimerSet", "LOCALS", 1)
		AreaCheck("AR6101")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_AR6101TimerSet", "LOCALS", 1)	
			SetGlobalTimer("XA_LC_AR6101Timer", "LOCALS", ONE_ROUND)
			SetInterrupt(TRUE)
			Continue()
	END

	IF 
		ActionListEmpty()
		Global("XA_LC_CanTalk", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_AR6101Timer", "LOCALS")
		GlobalLT("XA_LC_AR6101_Talk", "LOCALS", 1)
		AreaCheck("AR6101")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			DisplayStringHead(Myself, @26)	
			SetGlobal("XA_LC_AR6101_Talk", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END
//}

//{ Caelar Jailed
	IF 
		ActionListEmpty()
		Global("XA_LC_CorwinCaelarJail", "GLOBAL", 1)
		GlobalLT("XA_LC_CorwinCaelarJailTimerSet", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_CorwinCaelarJailTimer", "GLOBAL", TWO_ROUNDS)
			SetGlobal("XA_LC_CorwinCaelarJailTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
	END
	
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_CorwinCaelarJailTimer", "GLOBAL")
		GlobalLT("XA_LC_CorwinCaelarJailTalk", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinCaelarJailTalk", "GLOBAL", 1)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
//}

//{ Caelar Went Home
	IF 
		ActionListEmpty()
		Global("XA_LC_CorwinCaelarHome", "GLOBAL", 1)
		GlobalLT("XA_LC_CorwinCaelarHomeTimerSet", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_CorwinCaelarHomeTimer", "GLOBAL", TWO_ROUNDS)
			SetGlobal("XA_LC_CorwinCaelarHomeTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
	END
	
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_CorwinCaelarHomeTimer", "GLOBAL")
		GlobalLT("XA_LC_CorwinCaelarHomeTalk", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinCaelarHomeTalk", "GLOBAL", 1)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
//}

//{ Beno Exploded
	IF
		ActionListEmpty()
		InParty(Myself)
		GlobalTimerExpired("XA_LC_CorwinBenoSoul", "GLOBAL")
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalLT("XA_LC_CorwinBenoSoulTalk", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinBenoSoulTalk", "GLOBAL", 1)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
//}

//{ Entered Amkethran
	IF
		ActionListEmpty()
		InParty(Myself)
		AreaCheck("AR5500")
		!Global("XA_LC_AmkethranTimerSet", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_AmkethranTimer", "GLOBAL", ONE_ROUND)
			SetGlobal("XA_LC_AmkethranTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_AmkethranTimer", "GLOBAL")
		GlobalLT("XA_LC_MessageToBG", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			PlaySong(142)
			SetGlobal("XA_LC_MessageToBG", "GLOBAL", 1)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}

//{ Player gives soul fragment to Sarevok
	IF
		ActionListEmpty()
		GlobalGT("SarevokAlive", "GLOBAL", 0)
		GlobalLT("ImoenGiveSoul","GLOBAL",2)
		Global("XA_LC_CorwinRomanceActive","GLOBAL",2)
		!Global("XA_LC_SarevokSoulTimerSet", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_SarevokSoulTimer", "GLOBAL",TWO_DAYS)
			SetGlobal("XA_LC_SarevokSoulTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
//}

//{ Aerie Pregnant Check
	IF
		ActionListEmpty()
		GlobalGT("babytalk", "GLOBAL", 0)
		IsValidForPartyDialog("Aerie")
		!Global("XA_LC_CorwinAerieBabyTimerSet", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinAerieBabyTimerSet", "GLOBAL", 1)
			SetGlobalTimer("XA_LC_CorwinAerieBabyTimer", "GLOBAL", ONE_DAY)
			SetInterrupt(TRUE)
			Continue()
	END	
//}
	
//{ Entered Yaga-Shura Camp
	IF
		GlobalGT("YagaShuraHeart", "GLOBAL", 0)
		ActionListEmpty()
		GlobalLT("XA_LC_YagaTimerSet", "GLOBAL", 1)
		AreaCheck("AR5203")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_YagaTimer", "GLOBAL", ONE_ROUND)
			SetGlobal("XA_LC_YagaTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF 
		ActionListEmpty()
		AreaCheck("AR5203")
		Gender(Player1, MALE)
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
		GlobalGT("XA_LC_CorwinPregnant", "GLOBAL", 0)
		GlobalLT("XA_LC_PregnantOverride", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_PregnantOverride", "GLOBAL", 1)
			Continue()
	END
//}
	
//{ Sarevok Joined Party
	IF
		ActionListEmpty()
		InParty("Sarevok")
		InParty(Myself)
		GlobalLT("XA_LC_CorwinComplainSarevok", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_CorwinComplainSarevokTimer", "GLOBAL", ONE_HOUR)
			SetGlobal("XA_LC_CorwinComplainSarevok", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
//}

//{ Sarevok Interactions

	//{ Sarevok - <CHARNAME> Soul Interaction
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToSarevok", "LOCALS", 1)
		GlobalTimerExpired("XA_LC_SarevokSoulTimer", "GLOBAL")
		GlobalLT("XA_LC_SarevokSoul", "GLOBAL", 1)
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_SarevokSoul", "GLOBAL", 1)
			SetGlobal("XA_LC_ResetBanterTimer", "LOCALS", 1)
			Interact("Sarevok")
			SetInterrupt(TRUE)
	END
	//}

	//{ Banter Primer
	IF 
		ActionListEmpty()
		Global("XA_LC_CanTalkToSarevok", "LOCALS", 1)
		Global("XA_LC_Banter_NextInteraction", "LOCALS", 22) /* Sarevok */
		!Global("XA_LC_Banter_Pending", "LOCALS", 22)
		GlobalsGT("XA_LC_Banter_CorwinSarevokEnd", "XA_LC_Banter_CorwinSarevok")
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_Banter_Pending", "LOCALS", 22)
	END
	//}

	//{ Sarevok Banters
	IF
		ActionListEmpty()
		Global("XA_LC_Banter_Pending", "LOCALS", 22)
		Global("XA_LC_CanTalkToSarevok", "LOCALS", 1)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_ResetBanterTimer", "LOCALS", 1)
			IncrementGlobal("XA_LC_Banter_CorwinSarevok", "GLOBAL", 1)
			Interact("Sarevok")
			ActionOverride("Sarevok", StartDialog("SAREV25J", "XACORWIN"))
			SetInterrupt(TRUE)
	END
	//}
//} End Sarevok Interactions

//{ Caelar Interactions

	//{ Banter Primer
	IF 
		ActionListEmpty()
		Global("XA_LC_CanTalkToCaelar", "LOCALS", 1)
		Global("XA_LC_Banter_NextInteraction", "LOCALS", 23) /* Caelar */
		!Global("XA_LC_Banter_Pending", "LOCALS", 23)
		GlobalsGT("XA_LC_Banter_CorwinCaelarEnd", "XA_LC_Banter_CorwinCaelar")
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_Banter_Pending", "LOCALS", 23)
	END
	//}

	//{ Caelar Banters
	IF
		ActionListEmpty()
		Global("XA_LC_Banter_Pending", "LOCALS", 23)
		Global("XA_LC_CanTalkToCaelar", "LOCALS", 1)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_ResetBanterTimer", "LOCALS", 1)
			IncrementGlobal("XA_LC_Banter_CorwinCaelar", "GLOBAL", 1)
			Interact("XACAELAR")
			ActionOverride("XACAELAR", Interact("XACORWIN"))
			SetInterrupt(TRUE)
	END
	//}

//}


//{ Corwin Pregnant Interactions
	
	//{ Aerie Baby Parent Advice
	IF 
		ActionListEmpty()
		GlobalTimerExpired("XA_LC_CorwinAerieBabyTimer", "GLOBAL")
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		IsValidForPartyDialogue("Aerie")
		GlobalLT("XA_LC_CorwinParentAdvice", "GLOBAL", 1)
		GlobalGT("XA_LC_CorwinOpinionOfPlayer", "GLOBAL", 97)
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CorwinParentAdvice", "GLOBAL", 1)
			Continue()
	END
	
	IF
		Global("XA_LC_CorwinParentAdvice", "GLOBAL", 1)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		!Range(Player1, 6)
		ActionListEmpty()
	THEN
		RESPONSE #100
			MoveToObject(Player1)
			Continue()
	END
	
	IF
		Global("XA_LC_CorwinParentAdvice", "GLOBAL", 1)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		Range(Player1,7)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinParentAdvice", "GLOBAL", 2)
			//Dialog J-76
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Initial Timer Expiration
	IF 
		!AreaCheck("AR5203") // Yaga-Shura Camp
		Global("XA_LC_CorwinPregnant", "GLOBAL", 1)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		!ActuallyInCombat()
		OR(2)
			Global("XA_LC_PregnantOverride", "GLOBAL", 1)
			GlobalTimerExpired("XA_LC_CorwinPregnantTimer", "GLOBAL")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_PregnantOverride", "GLOBAL", 2)
			SetGlobalTimer("XA_LC_OneRoundTimer", "LOCALS", ONE_ROUND)
			SetGlobal("XA_LC_CorwinPregnant", "GLOBAL", 2)
			SetInterrupt(TRUE)
			Continue()
	END

	IF
		GlobalTimerExpired("XA_LC_OneRoundTimer", "LOCALS")
		Global("XA_LC_CorwinPregnant", "GLOBAL", 2)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			PlaySong(142)
			//Dialog J-71
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Second Interaction
	IF
		ActionListEmpty()
		Global("XA_LC_CorwinPregnant", "GLOBAL", 3)
		GlobalTimerExpired("XA_LC_CorwinPregnantTimer", "GLOBAL")
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregnant", "GLOBAL", 4)
			DisplayStringHead(Myself, @15)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		Global("XA_LC_CorwinPregnant", "GLOBAL", 4)
		!Range(Player1,6)
		ActionListEmpty()
	THEN
		RESPONSE #100
			MoveToObject(Player1)
			Continue()
	END
	
	IF
		Global("XA_LC_CorwinPregnant", "GLOBAL", 4)
		Range(Player1,7)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			PlaySong(146)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Third Interaction
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
		Global("XA_LC_CorwinPregnant", "GLOBAL", 5)
		OR(2)
			GlobalTimerExpired("XA_LC_CorwinPregnantTimer", "GLOBAL")
			GlobalTimerExpired("XA_LC_C3Timer", "LOCALS")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregnant", "GLOBAL", 6)
			PlaySong(142)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	

	//}
	
	//{ Fourth Interaction
	IF
		ActionListEmpty()
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
		Global("XA_LC_CorwinPregnant", "GLOBAL", 7)
		GlobalTimerExpired("XA_LC_CorwinPregnantTimer", "GLOBAL")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregnant", "GLOBAL", 8)
			PlaySong(142)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Final Interaction - Corwin Leaves
	IF
		ActionListEmpty()
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
		Global("XA_LC_CorwinPregnant", "GLOBAL", 9)
		GlobalTimerExpired("XA_LC_CorwinPregnantTimer", "GLOBAL")
	THEN
		RESPONSE #100
			SetGlobal("XA_LC_CorwinPregnant", "GLOBAL", 10)
			Continue()
	END
	
	IF
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		Global("XA_LC_CorwinRomanceActive", "GLOBAL", 2)
		Global("XA_LC_CorwinPregnant", "GLOBAL", 10)
		AreaCheck("AR4500")
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			PlaySong(146)
			StartDialog("XACORWIJ", Player1)
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Baby Name Interaction Imoen
	IF
		ActionListEmpty()
		GlobalTimerExpired("XA_LC_CorwinPregnantImoenTimer", "GLOBAL")
		GlobalLT("XA_LC_CorwinPregBabyName_Imoen", "GLOBAL", 1)
		Global("XA_LC_CanTalkToImoen2", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregBabyName_Imoen", "GLOBAL", 1)
			////DisplayStringHead(Myself, ~C~)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		Global("XA_LC_CorwinPregBabyName_Imoen", "GLOBAL", 1)
		Global("XA_LC_CanTalkToImoen2", "LOCALS", 1)
		Global("XA_LC_CanTalkToPlayer", "LOCALS", 1)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			//Dialog J-73
			SetInterrupt(FALSE)
			ActionOverride("Imoen2", StartDialog("IMOEN2J", Player1))
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Baby Name Interaction Aerie
	IF
		ActionListEmpty()
		Global("XA_LC_CorwinPregnant", "GLOBAL", 5)
		Global("XA_LC_CorwinPregnantAerie", "GLOBAL", 1)
		!Global("XA_LC_CorwinPregnantImoen", "GLOBAL", 1)
		!Global("XA_LC_CorwinPregAerieTimerSet", "GLOBAL", 1)
		InParty("Aerie")
		InParty(Myself)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_CorwinPregnantAerieTimer", "GLOBAL", TWO_DAYS)
			SetGlobal("XA_LC_CorwinPregAerieTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		GlobalTimerExpired("XA_LC_CorwinPregnantAerieTimer", "GLOBAL")
		Global("XA_LC_CanTalkToAerie", "LOCALS", 1)
		GlobalLT("XA_LC_CorwinPregBabyName_Aerie", "GLOBAL", 1)
		See(Player1)
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregBabyName_Aerie", "GLOBAL", 1)
			//Dialog J-74
			ActionOverride("Aerie", StartDialog("AERIEJ", Player1))
			SetInterrupt(TRUE)
	END
	//}
	
	//{ Baby Name Interaction Jaheira
	IF
		ActionListEmpty()
		Global("XA_LC_CorwinPregnant", "GLOBAL", 5)
		Global("XA_LC_CorwinPregnantJaheira", "GLOBAL", 1)
		!Global("XA_LC_CorwinPregnantImoen", "GLOBAL", 1)
		!Global("XA_LC_CorwinPregnantJahTimerSet", "GLOBAL", 1)
		InParty("Jaheira")
		InParty(Myself)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobalTimer("XA_LC_CorwinPregnantJahTimer", "GLOBAL", TWO_DAYS)
			SetGlobal("XA_LC_CorwinPregnantJahTimerSet", "GLOBAL", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	
	IF
		GlobalTimerExpired("XA_LC_CorwinPregnantJahTimer", "GLOBAL")
		IsValidForPartyDialogue("Jaheira")
		GlobalLT("XA_LC_CorwinPregnantBabyName_Jah", "GLOBAL", 1)
		See(Player1)
		See("Jaheira")
		!ActuallyInCombat()
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			SetGlobal("XA_LC_CorwinPregnantBabyName_Jah", "GLOBAL", 1)
			//Dialog J-75
			ActionOverride("Jaheira", StartDialog("JAHEIRAJ", Player1))
			SetInterrupt(TRUE)
	END
	//}
//}