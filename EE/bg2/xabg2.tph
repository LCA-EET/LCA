PRINT @3000  /* ~Processing BG2 changes...~ */
SILENT

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre_scripts/corwin~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/cre_scripts/shared~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg2/baf~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg2/baf/params~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg2/baf/cutscenes~
COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/bg2/d~

ACTION_IF (isEET = 0) THEN BEGIN
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~AUTINN_CREW~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~201 AUTINN_CREW~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~LUCILLA_CREW~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~202 LUCILLA_CREW~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~HOSTILES4~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~23 HOSTILES4~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~HOSTILES3~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~22 HOSTILES3~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~HOSTILES2~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~21 HOSTILES2~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~CRUSADERS~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~20 CRUSADERS~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ALLIES_NEUTRAL2~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~16 ALLIES_NEUTRAL2~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ALLIES_NEUTRAL~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~15 ALLIES_NEUTRAL~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ALLIES3~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~12 ALLIES3~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ALLIES2~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~11 ALLIES2~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ALLIES~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~10 ALLIES~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~THIEF~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~9 THIEF~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~NEUTRALS~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~19 NEUTRALS~
	END
	
	OUTER_SET specRef = IDS_OF_SYMBOL(SPECIFIC ~ORC~)
	ACTION_IF(specRef < 0) THEN BEGIN
		APPEND ~specific.ids~ ~18 ORC~
	END
END

INCLUDE ~%MOD_FOLDER%/generated/d/generated.tph~

ACTION_IF (bg1_carryover = 0) THEN BEGIN
	INCLUDE ~%MOD_FOLDER%/bg1/xabg1_co.tph~
END

ACTION_IF (sod_carryover = 0) THEN BEGIN
	INCLUDE ~%MOD_FOLDER%/sod/xasod_co.tph~
END

//{ Script Extensions
	EXTEND_TOP ~baldur.bcs~ 	~%MOD_FOLDER%/bg2/baf/xabalbg2.baf~
	EXTEND_TOP ~baldur.bcs~ 	~%MOD_FOLDER%/bg2/baf/xachfight.baf~
	EXTEND_TOP ~baldur.bcs~		~%MOD_FOLDER%/bg2/baf/xajrnbg2.baf~
	
	ACTION_IF (isEET = 0) THEN BEGIN
		EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/bg2/baf/xasdjrnl.baf~ // carryover SoD journal entries
	END
	
	EXTEND_BOTTOM ~baldur.bcs~		~%MOD_FOLDER%/bg2/baf/xasboost.baf~
	EXTEND_TOP ~player1d.bcs~ 	~%MOD_FOLDER%/bg2/baf/xap1d.baf~
	EXTEND_BOTTOM ~player1d.bcs~	~%MOD_FOLDER%/bg2/baf/xasexbg2.baf~
	EXTEND_BOTTOM ~player1d.bcs~	~%MOD_FOLDER%/bg2/baf/xasex.baf~
	EXTEND_TOP ~ppjon.bcs~ 		~%MOD_FOLDER%/bg2/baf/xappjon.baf~
	
	EXTEND_TOP ~bodhiamb.bcs~ 	~%MOD_FOLDER%/bg2/baf/xabodamb.baf~
	EXTEND_TOP ~cleanse.bcs~	~%MOD_FOLDER%/bg2/baf/xaclean.baf~
	EXTEND_TOP ~bcoffin.bcs~ 	~%MOD_FOLDER%/bg2/baf/xacoffin.baf~
	EXTEND_TOP ~ccgirl2.bcs~ 	~%MOD_FOLDER%/bg2/baf/xagirlov.baf~
	EXTEND_TOP ~valygar.bcs~ 	~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	EXTEND_TOP ~jaheira.bcs~ 	~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	EXTEND_TOP ~minsc.bcs~ 		~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	EXTEND_TOP ~mazzy.bcs~ 		~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	EXTEND_TOP ~aerie.bcs~ 		~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	EXTEND_TOP ~keldorn.bcs~ 	~%MOD_FOLDER%/bg2/baf/xasvir.baf~
	
	LAF xa_replace_recompile
		INT_VAR
			xa_includeInitial = 0
		STR_VAR
			xa_prefix = ~xapend~
			xa_extend = ~xapen~
			xa_bcs = ~xapend02~
	END
	
	LAF xa_replace_recompile
		INT_VAR
			xa_includeInitial = 0
		STR_VAR
			xa_prefix = ~xabbg0~
			xa_extend = ~xacs13~
			xa_bcs = ~xabbg02~
	END
//}

//{ Music	
	//{ SoD Track
		ADD_MUSIC xamu01	~%MOD_FOLDER%/bg2/mus/xamu01.mus~
		COPY ~%MOD_FOLDER%/bg2/mus/xamu01a.acm~ ~MUSIC/xamu01/xamu01a.acm~
	//}
	
	//{ Corwin Romance A
		ADD_MUSIC xamu02	~%MOD_FOLDER%/bg2/mus/xamu02.mus~
		COPY ~%MOD_FOLDER%/bg2/mus/xamu02a.acm~ ~MUSIC/xamu02/xamu02a.acm~
	//}
	
	//{ Corwin Romance B
		ADD_MUSIC xamu03	~%MOD_FOLDER%/bg2/mus/xamu03.mus~
		COPY ~%MOD_FOLDER%/bg2/mus/xamu03a.acm~ ~MUSIC/xamu03/xamu03a.acm~
	//}
	
	//{ Concern xamu06 ngoblina (SoD)
		ADD_MUSIC xamu09	~%MOD_FOLDER%/bg2/mus/xamu09.mus~
		COPY ~%MOD_FOLDER%/bg2/mus/xamu09a.acm~ ~MUSIC/xamu09/xamu09a.acm~
	//}
	
	COPY_EXISTING ~xacs09b.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~PlaySong(1003)~ ~PlaySong(%xamu03%)~
	END
	
	COPY_EXISTING ~xaclean.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~PlaySong(1003)~ ~PlaySong(%xamu03%)~
	END
	
	//{ Script Names
		ACTION_DEFINE_ARRAY xa_scriptNames
		BEGIN
			Aerie
			Anomen
			Cernd
			Dorn
			Edwin
			HaerDalis
			Hexxat
			Imoen2
			Jaheira
			Jan
			Keldorn
			Korgan
			Mazzy
			Minsc
			Nalia
			Neera
			Rasaad
			Sarevok
			Valygar
			Viconia
			Yoshimo
		END
	//}
	
	//{ Script Builders
		LAF xa_generate_talk_script END // xalctalk.bcs
		LAF xa_replace_recompile_string 
			INT_VAR
				xa_top = 0
			STR_VAR
				xa_strToReplace = ~\[NAME\]~
				xa_replacement = ~Corwin~
				xa_output = ~%MOD_FOLDER%/temp/xalcbico~
				xa_bcs = ~xalccs01~
		END
		
		LAF xa_build_banter_pending_script
			STR_VAR
				xa_replacement = ~Corwin~
				xa_bcs = ~xalccs03~
				xa_extendTo = ~xacor029~
		END
		LAF xa_build_banter_interaction
			STR_VAR
				xa_name = ~Corwin~
				xa_scriptName = ~XACORWIN~
				xa_outputbcs = ~xacor030~
		END
	//}
	
	//{ Corwin Script
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor011.baf~ // Rejoin Processing
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor005.baf~ // BG2 Misc
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor003.baf~ // Sound Adjustments
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor012.baf~ // Can Leave Check
		
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/shared/xalccs00.baf~ // Can Talk - Corwin / Player
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xalccs08.bcs~ // Talk to NPCs
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor027.bcs~ // Set banter end states
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/temp/xalcbico.bcs~ // Banter Interval - Corwin
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xalccs02.bcs~ // Banter Target Selector
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor029.bcs~ // Banter Pending
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor030.bcs~ // Banter Interaction
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xalccs05.bcs~ // Banter Target Selector Close
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor020.bcs~ // Non-Random BG2 Banters
		
		
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor013.baf~ // Rep Handling
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor004.baf~ // Opinion Baseline
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor007.baf~ // Romance Conflict Handling
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor008.baf~ // BG2 Event Handling
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor009.baf~ // RBG Event Handling
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor023.baf~ // BG2 NPC Interactions
		
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/corwin/xacor006.baf~ // Exp Boost Spellhold
		EXTEND_BOTTOM ~xacorwin.bcs~ ~%MOD_FOLDER%/cre_scripts/shared/xalccs06.baf~ // Exp Boost Join
	//}
	
	ACTION_IF (isEET = 1) THEN BEGIN
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor002.baf~ // Corwin Stat Matching
		EXTEND_BOTTOM ~xacorwin.bcs~ ~override/xacor026.baf~ // Corwin SoD Item Check
	END
	
	COPY_EXISTING ~xacorwin.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~PlaySong(1001)~ ~PlaySong(%xamu01%)~
		REPLACE_TEXTUALLY ~PlaySong(1002)~ ~PlaySong(%xamu02%)~
		REPLACE_TEXTUALLY ~PlaySong(1003)~ ~PlaySong(%xamu03%)~
		REPLACE_TEXTUALLY ~PlaySong(1009)~ ~PlaySong(%xamu09%)~
	END
	
	COPY_EXISTING ~xalcdbg.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~PlaySong(1001)~ ~PlaySong(%xamu01%)~
		REPLACE_TEXTUALLY ~PlaySong(1002)~ ~PlaySong(%xamu02%)~
		REPLACE_TEXTUALLY ~PlaySong(1003)~ ~PlaySong(%xamu03%)~
		REPLACE_TEXTUALLY ~PlaySong(1009)~ ~PlaySong(%xamu09%)~
	END
//}

//{ bmp
	COPY ~%MOD_FOLDER%/bg2/bmp~ ~override~
//}

//{ are
	COPY ~%MOD_FOLDER%/bg2/are~ ~override~
	
	COPY_EXISTING ~xacs06.are~ ~override~
	LPF xa_rescript_actors 
		STR_VAR
			xa_override = ~xalcnull~
			xa_class = ~xalcnull~
			xa_race = ~xalcnull~
			xa_general = ~xalcnull~
			xa_default = ~xalcnull~
			xa_specifics = ~xalcnull~	
	END // remove any existing scripts on the actors in the scene

	COPY_EXISTING ~xacs13.are~ ~override~
	LPF fj_are_structure
		INT_VAR
			fj_box_left		= 249
			fj_box_top		= 579
			fj_box_right	= 250
			fj_box_bottom	= 580
			fj_cursor_idx	= 0
			fj_alt_x		= 0
			fj_alt_y		= 0
			fj_flags		= 0
			fj_vertex_0     = 249 + (579 << 16)
			fj_vertex_1     = 249 + (580 << 16)
			fj_vertex_2     = 250 + (580 << 16)
			fj_vertex_3     = 250 + (579 << 16)
			fj_type			= 1
		STR_VAR
			fj_structure_type = region
			fj_name = ~ExitNull~
	END

	COPY_EXISTING ~xad100.are~ ~override~ // bd0103, Ducal Palace 3 Fl
	LPF xa_delete_actor_at_index END
	
	COPY_EXISTING ~xad101.are~ ~override~ // bd0104, FFHQ 1st Floor
	WRITE_LONG 0x5417 1	// Open Door07
	LPF xa_delete_are_actors 
			INT_VAR 
				xa_component_count = 2 // will delete Tiax & Corwin
	END
	
	COPY_EXISTING ~xablck.are~ ~override/xapen.are~
	LPF xa_delete_are_actors END
	WRITE_ASCII 0x94 ~xapen~ #8
	WRITE_LONG 0x11C 0
	WRITE_LONG 0x120 0
	WRITE_LONG 0x124 0
	WRITE_LONG 0x128 0
	WRITE_LONG 0x12C 0
//}

//{ Modifications to already-existing areas
	//{ Area script extensions
		EXTEND_TOP ~ar0334.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar0334.baf~
		EXTEND_TOP ~ar0602.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar0602.baf~
		EXTEND_TOP ~ar0809.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar0809.baf~
		EXTEND_TOP ~ar1002.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar1002.baf~
		EXTEND_TOP ~ar1512.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar1512.baf~
		EXTEND_TOP ~ar1516.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar1516.baf~
		EXTEND_TOP ~ar2000.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar2000.baf~
		EXTEND_TOP ~ar2802.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar2802.baf~
		EXTEND_TOP ~ar2900.bcs~ 	~%MOD_FOLDER%/bg2/baf/xaar2900.baf~
		EXTEND_BOTTOM ~ohgorge.bcs~	~%MOD_FOLDER%/bg2/baf/xagorge.baf~
		
	//}
	
	//{ If the playthrough is started in BG2, apply the default or overriden parameters (even for EET games).
		EXTEND_TOP ~ar0602.bcs~		~%MOD_FOLDER%/bg2/baf/xalcaeet.baf~
	//}
	
	//{ Bodhi's Hideout
		COPY_EXISTING ~AR0801.ARE~ ~override~
		LPF fj_are_structure
			INT_VAR
			fj_type 		= 0
			fj_box_left		= 2230
			fj_box_top		= 550
			fj_box_right	= 2400
			fj_box_bottom	= 750
			fj_cursor_idx	= 0
			fj_alt_x		= 0
			fj_alt_y		= 0
			fj_flags		= 0
			fj_vertex_0     = 2230 + (550 << 16)
			fj_vertex_1     = 2400 + (550 << 16)
			fj_vertex_2     = 2400 + (750 << 16)
			fj_vertex_3     = 2230 + (750 << 16)
			STR_VAR
			fj_structure_type = region
			fj_name = ~Corwin Trigger~
			fj_reg_script = ~XA801T0~
		END

		LPF fj_are_structure
			INT_VAR
			fj_type 		= 0
			fj_box_left		= 575
			fj_box_top		= 740
			fj_box_right	= 710
			fj_box_bottom	= 790
			fj_cursor_idx	= 0
			fj_alt_x		= 0
			fj_alt_y		= 0
			fj_flags		= 0
			fj_vertex_0     = 575 + (740 << 16)
			fj_vertex_1     = 710 + (740 << 16)
			fj_vertex_2     = 710 + (790 << 16)
			fj_vertex_3     = 575 + (790 << 16)
			STR_VAR
			fj_structure_type = region
			fj_name = ~Corwin Trigger2~
			fj_reg_script = ~XA801T0~
		END
	//}
//}

//{ cre
	COPY ~%MOD_FOLDER%/bg2/wav~ ~override~
	COPY ~%MOD_FOLDER%/bg2/wav/corwin~ ~override~

	//{ xacorwin
		COPY_EXISTING ~xad106.cre~ ~override/xacorwin.cre~ // corwin7
		REMOVE_CRE_ITEMS
		LPF xa_reset_cre
			STR_VAR
				xa_dialog = ~xacorwin~
				xa_scriptName = ~xacorwin~
				xa_override = ~xacorwin~
				xa_class = ~xafigh01~
		END	
	//}
	
	COPY_EXISTING ~xalcspy.cre~ ~override/xacorinv.cre~
	WRITE_ASCII 0x248 ~xacorinv~ #8
	WRITE_ASCII 0x280 ~xacorinv~ #20
	
	COPY_EXISTING ~xalcspy.cre~ ~override/xacrowd.cre~
	WRITE_LONG 0x8 RESOLVE_STR_REF(@3188)
	WRITE_LONG 0xC RESOLVE_STR_REF(@3188)	
	
	COPY_EXISTING ~suelf2.cre~ ~override/xabowyer.cre~
	WRITE_ASCII 0x280 ~xabowyer~ #20
	WRITE_SHORT 0x24 45
	WRITE_SHORT 0x26 45
	WRITE_ASCII 0x2CC ~xabowsmi~ #8
	WRITE_ASCII 0x250 ~xabowsmi~ #8
	WRITE_LONG 0x8 RESOLVE_STR_REF(@3055) // Master Bowyer
	WRITE_LONG 0xC RESOLVE_STR_REF(@3055) // Master Bowyer
	
	COPY_EXISTING ~suelf7.cre~ ~override/xaesmith.cre~
	WRITE_ASCII 0x280 ~xaesmith~ #20
	WRITE_SHORT 0x24 45
	WRITE_SHORT 0x26 45
	WRITE_ASCII 0x2CC ~xabowsmi~ #8
	WRITE_ASCII 0x258 ~xabowsmi~ #8
	WRITE_LONG 0x8 RESOLVE_STR_REF(@3036) // Master Smith
	WRITE_LONG 0xC RESOLVE_STR_REF(@3036) // Master Smith

	COPY_EXISTING ~xad105.cre~ ~override/xamelkor.cre~ // bdffwiz
	SAY NAME1 @3148
	SAY NAME2 @3148
	WRITE_ASCII 0x280 ~xamelkor~ #20 // Script Name
	WRITE_ASCII 0x248 ~xamelkor~ #8 // override
	WRITE_ASCII 0x2CC ~xamelkor~ #8 // dialog
	
	COPY_EXISTING ~cat.cre~ ~override/xadings.cre~
	SAY NAME1 @3016
	SAY NAME2 @3016
	WRITE_ASCII 0x34 ~xadingss~ #8
	WRITE_ASCII 0x248 ~xadings~ #8
	WRITE_ASCII 0x280 ~xadings~ #20
	WRITE_ASCII 0x2CC ~xadings~ #8

	COPY_EXISTING ~xad121.cre~ ~override/xaffofcr.cre~ //bdffgirl
	WRITE_ASCII 0x280 ~xaffofcr~ #20
	WRITE_ASCII 0x2CC ~xaffofcr~ #8
	SAY NAME1 @3058
	SAY NAME2 @3058
	
	COPY ~%MOD_FOLDER%/bg2/cre/xaffval1.cre~ ~override~
	SAY NAME1 					@3169 /*~Flaming Fist Valiant~*/
	SAY NAME2 					@3169 /*~Flaming Fist Valiant~*/
	WRITE_LONG 200 RESOLVE_STR_REF(@10983) /*~Punish the lawbreakers!~[xad10004]*/
	WRITE_LONG 268 RESOLVE_STR_REF(@10984) /*~We're more than mercenaries.~[xad10005]*/
	WRITE_LONG 272 RESOLVE_STR_REF(@10985) /*~War is almost here.~[xad10006]*/
	
	COPY_EXISTING ~xaffval1.cre~ ~override/xaffval2.cre~
	WRITE_ASCII 0x280 ~XAFFVAL2~ #20  	// Script Name
	
	COPY_EXISTING ~xaffval1.cre~ ~override/xaffval3.cre~
	WRITE_ASCII 0x280 ~XAFFVAL3~ #20  	// Script Name
	
	COPY_EXISTING ~golsto01.cre~ ~override/xagolsto.cre~
	WRITE_BYTE 0x270 128 // set allegiance to neutral
	WRITE_ASCII 0x248 ~xalcnull~ #8
	WRITE_ASCII 0x268 ~xalcnull~ #8
	WRITE_ASCII 0x280 ~xagolsto~ #20
	WRITE_ASCII 0x2CC ~xagolsto~ #8
	
	COPY_EXISTING ~xalcspy.cre~ ~override/xalchook.cre~
	WRITE_ASCII 0x34 ~xaschaes~ #8
	WRITE_ASCII 0x248 ~xalchook~ #8
	WRITE_ASCII 0x280 ~xalchook~ #20
	WRITE_ASCII 0x2CC ~xalchook~ #8
		
	COPY_EXISTING ~golsto01.cre~ ~override/xatargol.cre~
	WRITE_ASCII 0x268 ~xalcnull~ #8
	WRITE_ASCII 0x280 ~TargetGolem~ #20
	SAY NAME1 @3071
	SAY NAME2 @3071
	
	COPY_EXISTING ~trftow04.cre~ ~override/xatmarsu.cre~
	WRITE_ASCII 0x248 ~xatmarsu~ #8
	WRITE_ASCII 0x280 ~xatmarsu~ #20
	WRITE_ASCII 0x2CC ~xatmarsd~ #8
	
	COPY_EXISTING ~AMMAG01.cre~ ~override/xawapren.cre~
	SAY NAME1 @3149
	SAY NAME2 @3149
	WRITE_ASCII 0x280 ~xawapren~ #20
	WRITE_ASCII 0x248 ~xalcnull~ #8	
	
	COPY ~%MOD_FOLDER%/bg2/cre/xaskcut2.cre~ ~override~
	SAY NAME1 @3070
	SAY NAME2 @3070
//}

//{ Block D Modifications
	COPY_EXISTING ~xad100.cre~ ~override/xabelt.cre~ // bdbelt
	WRITE_ASCII 0x280 ~xabelt~ #20  	// Script Name
	WRITE_ASCII 0x2CC ~xabelt~ #8		// Dialogue 
	
	COPY_EXISTING ~xad101.cre~ ~override/xaeltan.cre~ // bdeltan
	WRITE_ASCII 0x280 ~xaeltan~ #20  	// Script Name
	WRITE_ASCII 0x2CC ~xaeltan~ #8		// Dialogue
	
	COPY_EXISTING ~xad102.cre~ ~override/xaentar.cre~ // bdentar
	WRITE_ASCII 0x280 ~xaentar~ #20  	// Script Name
	WRITE_ASCII 0x2CC ~xaentar~ #8		// Dialogue
	
	COPY_EXISTING ~xad103.cre~ ~override/xaliia.cre~ // bdliia
	WRITE_ASCII 0x280 ~xaliia~ #20  	// Script Name
	WRITE_ASCII 0x2CC ~xaliia~ #8		// Dialogue
	
	COPY_EXISTING ~xad104.cre~ ~override/xabence.cre~ // bdbence
	WRITE_ASCII 0x280 ~xabence~ #20  	// Script Name
	WRITE_ASCII 0x2CC ~xabence~ #8		// Dialogue
	WRITE_ASCII 0x248 ~xabence~ #8		// Override Script
	
	COPY_EXISTING ~xad109.cre~ ~override/xaschael.cre~ // bdschael
	WRITE_ASCII 0x280 ~xaschael~ #20  	// Script Name
//}

OUTER_SET bg2_carryover = 1

//{ Gesen Projectile
	ADD_PROJECTILE ~%MOD_FOLDER%/bg2/pro/xalarrow.pro~
//}

//{ BAM
	COPY ~%MOD_FOLDER%/bg2/bam~ ~override~
//}

//{ New Items
	//{ Corwin's Bag of Holding
		COPY_EXISTING ~BAG04.ITM~ ~override/xacorbag.itm~
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3190)
		WRITE_ASCII 0x3A ~xacorbag~ #8
		
		COPY_EXISTING ~BAG04.STO~ ~override/xacorbag.sto~
		WRITE_SHORT 0x22 50 // store up to 50 items
		ADD_STORE_ITEM "misc42" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc43" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc41" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc36" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc44" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc38" #1 #0 #0 ~IDENTIFIED~ #1
		ADD_STORE_ITEM "misc07" #10000 #0 #0 ~IDENTIFIED~ #1
	//}
	
	//{ Corwin's Ammo Belt
		COPY_EXISTING ~BAG05.ITM~ ~override/xacorblt.itm~
		COPY_EXISTING ~BAG05.STO~ ~override/xacorblt.sto~
		ADD_STORE_ITEM "arow01" #1 #0 #0 ~IDENTIFIED~ #80
		ADD_STORE_ITEM "arow02" #1 #0 #0 ~IDENTIFIED~ #80
		ADD_STORE_ITEM "arow09" #1 #0 #0 ~IDENTIFIED~ #40
	//}

	//{ Corwin's Body
		COPY_EXISTING ~MISCBL.itm~ ~override/xacorbod.itm~
		WRITE_LONG 0x4C 155 // Weight
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3138)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3138)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3139)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3139)
		WRITE_ASCII 0x3A ~xacorbod~ #8
		WRITE_ASCII 0x44 ~xagcorbd~ #8
	//}
	
	//{ Letter to Duncan
		COPY_EXISTING ~scrl2i.itm~  ~override/xaldunc.itm~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3140)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3140)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3144)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3144)
	//}
	
	//{ Letter to Duncan (Cost)
		COPY_EXISTING ~scrl2i.itm~  ~override/xaldunca.itm~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3140)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3140)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3147)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3147)
	//}
	
	//{ Letter to Corwin, Romantic
		COPY_EXISTING ~scrl2i.itm~  ~override/xalcorw1.itm~ 
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3142)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3142)
	//}
	
	//{ Letter to Corwin, Friend
		COPY_EXISTING ~scrl2i.itm~  ~override/xalcorw2.itm~ 
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3143)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3143)
	//}
	
	//{ Letter to Corwin, Romantic (Cost)
		COPY_EXISTING ~scrl2i.itm~  ~override/xalcor1a.itm~ 
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3145)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3145)
	//}
	
	//{ Letter to Corwin, Friend (Cost)
		COPY_EXISTING ~scrl2i.itm~  ~override/xalcor2a.itm~ 
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3141)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3146)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3146)
	//}
		
	//{ Corwin's Amulet (SoA)
		COPY ~%MOD_FOLDER%/bg2/itm/xaamul2.itm~ ~override~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3150)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3150)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3151)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3151)
		WRITE_LONG 0x9E RESOLVE_STR_REF(@3152)
	//}
	
	//{ Corwin's Bow + 2
		COPY_EXISTING ~xad107.itm~ ~override/xacorbow.itm~ // bdbow06
		WRITE_LONG 0xC RESOLVE_STR_REF(@3057)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3154)
		WRITE_ASCII 0x1AE ~xacorwin~ #8
		WRITE_SHORT 0x80 109 // Set range to 105
		WRITE_BYTE 0x84 5 // Lower Speed from 6 to 5
		WRITE_SHORT 0x86 3 // Increase to-hit bonus from 2 to 3
		WRITE_SHORT 0x8C 4 // Increase damage bonus from 3 to 4
		WRITE_LONG 0x60 2 // Increase enchantment from 1 to 2
		WRITE_BYTE 0x1DC 15 // Increase entangle chance to 15%
		WRITE_LONG 0x1C6 RESOLVE_STR_REF(@3152)
	//}
	
	//{ Corwin's Bow + 3 
		COPY_EXISTING ~xacorbow.itm~ ~override/xacorbo3.itm~
		WRITE_LONG 0xC RESOLVE_STR_REF(@3163)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3166)
		WRITE_SHORT 0x80 109 // Set range to 109
		WRITE_BYTE 0x84 4 // Lower Speed from 5 to 4
		WRITE_SHORT 0x86 4 // Increase to-hit bonus from 3 to 4
		WRITE_SHORT 0x8C 5 // Increase damage bonus from 4 to 5
		WRITE_LONG 0x60 3 // Increase enchantment from 2 to 3
		WRITE_BYTE 0x1DC 19 // Increase entangle chance to 19%
	//}
	
	//{ Corwin's Bow + 3 (Gesen)
		COPY ~%MOD_FOLDER%/bg2/itm/xacorbg3.itm~ ~override~
		WRITE_SHORT 0x09c ~%xalarrow%~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3163)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3163)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3164)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3164)
		WRITE_LONG 0x25E RESOLVE_STR_REF(@3152)
	//}
	
	//{ Corwin's Bow + 4 (Gesen)
		COPY_EXISTING ~xacorbg3.itm~ ~override/xacorbg4.itm~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3167)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3167)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3168)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3168)
		WRITE_LONG 0x60 4 // Increase enchantment from 3 to 4
		WRITE_SHORT 0x80 80 // Increase range to 80
		WRITE_SHORT 0xB8 80 // Increase range to 80
		WRITE_SHORT 0xBE 5 // Set to-hit bonus
		WRITE_SHORT 0xC4 6 // Set damage bonus
	//}
	
	//{ Corwin's Armor + 3
		COPY_EXISTING ~xad106.itm~ ~override/xacorchn.itm~ // bdchan05
		WRITE_LONG 0x8 RESOLVE_STR_REF(@3155)
		WRITE_LONG 0xC RESOLVE_STR_REF(@3155)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3156)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3156)
		WRITE_LONG 0x2AE RESOLVE_STR_REF(@3152)
		WRITE_LONG 0x166 2 // Lower Base AC from 3 to 2
		WRITE_LONG 0x225 15 // Increase Magic Resistance from 10% to 15%
		WRITE_ASCII 0x296 ~xacorwin~ #8	
	//}
	
	//{ Corwin's Armor + 4
		COPY_EXISTING ~xacorchn.itm~ ~override/xacorch4.itm~
		WRITE_LONG 0x08 RESOLVE_STR_REF(@3161)
		WRITE_LONG 0x0C RESOLVE_STR_REF(@3161)
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3162)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3162)
		WRITE_LONG 0x2AE RESOLVE_STR_REF(@3152)
		WRITE_LONG 0x166 1 // Lower Base AC from 2 to 1
		LPF ADD_ITEM_EFFECT // +1 Saving Throw Bonus
			INT_VAR
				opcode = 325
				target = 1
				power = 1
				timing = 2
				parameter1 = 1
				parameter2 = 0
				savebonus = 1 
		END
	//}
	
	//{ Soultaker Dagger
		COPY_EXISTING ~dagg10.itm~ ~override/xastdag.itm~				
		WRITE_LONG 0x50 RESOLVE_STR_REF(@3159)
		WRITE_LONG 0x54 RESOLVE_STR_REF(@3160)
	//}

//}

//{ New Spells
	COPY  ~%MOD_FOLDER%/bg2/spl/xasboost.spl~ ~override~
	WRITE_LONG 0x08 RESOLVE_STR_REF(@3187)

	COPY ~%MOD_FOLDER%/bg2/spl/xachng01.spl~ ~override~
	WRITE_LONG 0x08 RESOLVE_STR_REF(@3136)
	
	COPY ~%MOD_FOLDER%/bg2/spl/xachng02.spl~ ~override~
	WRITE_LONG 0x08 RESOLVE_STR_REF(@3137)
	
	COPY ~%MOD_FOLDER%/bg2/spl/xagameov.spl~ ~override~
	WRITE_LONG 0x08 RESOLVE_STR_REF(@3170)
	
	COPY ~%MOD_FOLDER%/bg2/spl/xacorbo3.spl~ ~override~
	COPY ~%MOD_FOLDER%/bg2/spl/xahide.spl~ ~override~
	COPY ~%MOD_FOLDER%/bg2/spl/xaunhide.spl~ ~override~
	
	COPY ~%MOD_FOLDER%/bg2/spl/xadisese.spl~ ~override~
	WRITE_LONG 0x08 RESOLVE_STR_REF(@3189)
//}

//{ 2DA Modifications
	//{ XACORWIN
		COPY_EXISTING ~pdialog.2da~ ~override~
		INSERT_2DA_ROW 0 8 ~XACORWIN XACORWIP XACORWIJ XACORWID XACOR25P XACOR25J XACOR25D XACOR25~

		COPY_EXISTING ~interdia.2da~ ~override~
		INSERT_2DA_ROW 0 3 ~XACORWIN XACORWIB XACOR25B~
	//}
//}

//{ Parameters
	/* 
		Set Default Parameters
	*/

	OUTER_SET player_exiled = 1
	OUTER_SET corwin_romance = 1
	OUTER_SET caelar_alive = 1
	OUTER_SET drinks_with_corwin = 1
	OUTER_SET ashatiel_duel = 1
	OUTER_SET saved_jhasso = 1
	OUTER_SET saved_aldeth = 1
	OUTER_SET aldeth_ff = 0
	OUTER_SET met_rohma = 1
	OUTER_SET sacrifice_corwin = 0
	OUTER_SET neoma_earrings = 1
	OUTER_SET happy_ending = 1
	OUTER_SET tricked_larze = 1
	OUTER_SET helped_forthel = 1
	OUTER_SET met_kerrachus = 1
	OUTER_SET stole_farseer = 0
	OUTER_SET helped_brielbara = 1
	OUTER_SET brielbara_coran = 1
	OUTER_SET fergus_ring = 1
	OUTER_SET cleared_kanaglym = 1
	OUTER_SET corwin_avernus = 1
	OUTER_SET thrix_alive = 1
	
	//{ Blackhearts Compatibility
		OUTER_SET xa_bhcompat = 0
		ACTION_IF (MOD_IS_INSTALLED "setup-Blackhearts EET.tp2" "0") THEN BEGIN
			INCLUDE ~%MOD_FOLDER%/compat/bh_eet/bh_eet.tph~
		END
		ACTION_IF (MOD_IS_INSTALLED "setup-Blackhearts EET.tp2" "0") THEN BEGIN
			ACTION_IF (xa_bhcompat = 0) THEN BEGIN
				INCLUDE ~%MOD_FOLDER%/compat/bh_eet/bh_eet.tph~
			END
		END
	//}
	
	/*
		If a parameter file exists, overwrite the default parameter with the contents of the parameter file.
	*/
	ACTION_IF (FILE_EXISTS ~%MOD_FOLDER%/bg2/lca_bg2_params.txt~) THEN BEGIN
		INCLUDE ~%MOD_FOLDER%/bg2/lca_bg2_params.txt~
	END

	ACTION_IF (player_exiled = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa00.baf~
	END

	ACTION_IF (corwin_romance = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa01.baf~
	END ELSE BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa02.baf~
	END

	ACTION_IF (caelar_alive = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa03.baf~
	END

	ACTION_IF (drinks_with_corwin = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa04.baf~
	END

	ACTION_IF (ashatiel_duel = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa05.baf~
	END

	ACTION_IF (saved_jhasso = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa06.baf~
	END

	ACTION_IF (saved_aldeth = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa07.baf~
	END

	ACTION_IF (aldeth_ff = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa08.baf~
	END

	ACTION_IF (met_rohma = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa09.baf~
	END

	ACTION_IF (sacrifice_corwin = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa10.baf~
	END

	ACTION_IF (neoma_earrings = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa11.baf~
	END

	ACTION_IF (happy_ending = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa12.baf~
	END

	ACTION_IF (tricked_larze = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa13.baf~
	END

	ACTION_IF (helped_forthel = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa14.baf~
	END

	ACTION_IF (met_kerrachus = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa15.baf~
	END

	ACTION_IF (stole_farseer = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa16.baf~
	END

	ACTION_IF (helped_brielbara = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa17.baf~
	END

	ACTION_IF (brielbara_coran = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa18.baf~
	END

	ACTION_IF (fergus_ring = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa19.baf~
	END

	ACTION_IF (cleared_kanaglym = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa20.baf~
	END
	
	ACTION_IF (corwin_avernus = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa21.baf~
	END
	
	ACTION_IF (thrix_alive = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa22.baf~
	END

	ACTION_IF (corwin_lesson = 1) THEN BEGIN
		EXTEND_TOP ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpa23.baf~
	END

	EXTEND_BOTTOM ~AR0602.bcs~ ~%MOD_FOLDER%/bg2/baf/params/xalcpaEE.baf~
//}

//{ Place Corwin's Amulet and Letter in Irenicus' stronghold, if the player has it at the end of SoD. Otherwise, they will be copied by the overridden AR0602 script, but only if the player started their game in BG2 (pursuant to the provided parameters).
	
	COPY_EXISTING ~IMPORT01.2DA~ ~override~
	COUNT_2DA_ROWS 2 import1rows
	INSERT_2DA_ROW (import1rows) 2 ~%import1rows% XALTRCOR~
	
	COPY_EXISTING ~IMPORT02.2DA~ ~override~
	COUNT_2DA_ROWS 2 import2rows
	INSERT_2DA_ROW (import2rows) 2 ~%import2rows% XALTRSCH~
	
	COPY_EXISTING ~IMPORT03.2DA~ ~override~
	COUNT_2DA_ROWS 2 import3rows
	INSERT_2DA_ROW (import3rows) 2 ~%import3rows% XAAMULET~
//}

//{ Mod Compatibility
	//{ Cost of One Girl's Soultaker
		ACTION_IF !(MOD_IS_INSTALLED "Setup-SkieCost.tp2" "0") THEN BEGIN
			PRINT ~Cost of One Girl's Soul is not installed. IDJINNI will be patched accordingly.~
			SILENT
			STRING_SET 10873 @3171 //IDJINNI State 1
			STRING_SET 10883 @3172 //IDJINNI State 1 Transition 0
			STRING_SET 10883 @3172 //IDJINNI State 2 Transition 0
			STRING_SET 10894 @3176 //IDJINNI State 3
			STRING_SET 10900 @3172 //IDJINNI State 3 Transition 0
			STRING_SET 10906 @3173 //IDJINNI State 4
			STRING_SET 10908 @3174 //IDJINNI State 4 Transition 0
			STRING_SET 10912 @3177 //IDJINNI State 5
			STRING_SET 10948 @3175 //IDJINNI State 6
			STRING_SET 10995 @3178 //IDJINNI State 8
			STRING_SET 11001 @3179 //IDJINNI State 9
			STRING_SET 11002 @3180 //IDJINNI State 10
			STRING_SET 11004 @3181 //IDJINNI State 11
			STRING_SET 19801 @3182 //IDJINNI State 15
			STRING_SET 19916 @3185 //IDJINNI State 17
			STRING_SET 19930 @3186 //IDJINNI State 19
			STRING_SET 44816 @3183 //IDJINNI State 20
			STRING_SET 47515 @3184 //IDJINNI State 12 Journal Entry		
			COMPILE ~%MOD_FOLDER%/compat/cgs/d/xalccgs.d~ USING ~%MOD_FOLDER%/compat/cgs/tra/english/xalccgs.tra~	
		END ELSE BEGIN
			PRINT ~Cost of One Girl's Soul is installed.~
			SILENT
			
			ACTION_IF FILE_EXISTS_IN_GAME ~l#2eddrd.dlg~ THEN BEGIN
				COMPILE ~%MOD_FOLDER%/compat/cgs/d/xalced.d~ USING ~%MOD_FOLDER%/compat/cgs/tra/english/xalced.tra~
			END
			ACTION_IF FILE_EXISTS_IN_GAME ~l#2sdski.dlg~ THEN BEGIN
				COMPILE ~%MOD_FOLDER%/compat/cgs/d/xalcsk.d~ USING ~%MOD_FOLDER%/compat/cgs/tra/english/xalcsk.tra~
			END
		END
	//}
//}