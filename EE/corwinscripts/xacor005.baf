//Note: Variable Key Max Length Appears to be 32.

//{ 00: Blocking Actions
	IF
		Global("XA_LC_CorwinIrenicus", "GLOBAL", 4)
	THEN
		RESPONSE #100
			////DisplayStringHead(Myself, ~CI4 Script Blocking~)
			NoAction()
	END
//}

//{ 01: Amulet / Letter on Party Join
	
	//{ Include the letter sent by the Player
	IF
		InParty(Myself)
		Global("XA_LC_LetterSent", "GLOBAL", 1)
		GlobalLT("XA_LC_CreatedLetterInventory", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			CreateItem("XALCORW1",0,0,0)
			SetGlobal("XA_LC_CreatedLetterInventory", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END

	IF
		InParty(Myself)
		Global("XA_LC_LetterSent", "GLOBAL", 2)
		GlobalLT("XA_LC_CreatedLetterInventory", "LOCALS", 1)
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			CreateItem("XALCORW2",0,0,0)
			SetGlobal("XA_LC_CreatedLetterInventory", "LOCALS", 1)
			SetInterrupt(TRUE)
			Continue()
	END
	//}

	//{ Corwin Amulet 
	IF
		InParty(Myself)
		PartyHasItem("XAAMULET")	
	THEN
		RESPONSE #100
			SetInterrupt(FALSE)
			DestroyItem("XAAMULET")
			ActionOverride(Player1, DestroyItem("XAAMULET"))
			ActionOverride(Player2, DestroyItem("XAAMULET"))
			ActionOverride(Player3, DestroyItem("XAAMULET"))
			ActionOverride(Player4, DestroyItem("XAAMULET"))
			ActionOverride(Player5, DestroyItem("XAAMULET"))
			ActionOverride(Player6, DestroyItem("XAAMULET"))
			SetInterrupt(TRUE)
			Continue()
	END
	//}

//}



//{ 19: Corwin Turn Hostile
IF 
	InParty(Myself)
	Global("XA_LC_CorwinHostile", "GLOBAL", 1)
THEN 
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LC_CorwinHostile", "GLOBAL", 2)
		DisplayStringHead(Myself, @11)
		SetLeavePartyDialogFile()
		ChangeAIScript("", DEFAULT)
		LeaveParty()
		Enemy()
		SetInterrupt(TRUE)
END
//}

//{ Check for old SoD items
IF 
	InParty(Myself)
	OR(2)
		GlobalLT("XA_LC_SoDItemCheckTimerSet", "GLOBAL", 1)
		GlobalTimerExpired("XA_LC_SoDItemCheckTimer", "GLOBAL")
	OR(2)
		PartyHasItem("BDBOW06") // Corwin's Bow (SoD)
		PartyHasItem("BDCHAN05") // Corwin's Armor (SoD)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LC_SoDItemCheckTimer", "GLOBAL", ONE_DAY)
		SetGlobal("XA_LC_SoDItemCheckTimerSet", "GLOBAL", 1)
		
		ActionOverride(Player1, DestroyItem("BDBOW06"))
		ActionOverride(Player2, DestroyItem("BDBOW06"))
		ActionOverride(Player3, DestroyItem("BDBOW06"))
		ActionOverride(Player4, DestroyItem("BDBOW06"))
		ActionOverride(Player5, DestroyItem("BDBOW06"))
		ActionOverride(Player6, DestroyItem("BDBOW06"))
		
		ActionOverride(Player1, DestroyItem("BDCHAN05"))
		ActionOverride(Player2, DestroyItem("BDCHAN05"))
		ActionOverride(Player3, DestroyItem("BDCHAN05"))
		ActionOverride(Player4, DestroyItem("BDCHAN05"))
		ActionOverride(Player5, DestroyItem("BDCHAN05"))
		ActionOverride(Player6, DestroyItem("BDCHAN05"))
		SetInterrupt(TRUE)
		
		Continue()
END
//}
