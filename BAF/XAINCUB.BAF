IF
	Global("XA_IncubusHostile", "LOCALS", 1)
	Global("XA_PrisonKnowledge", "GLOBAL", 6)
	!Global("XA_IncubusBuff", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_IncubusBuff", "LOCALS", 1)
		ClearActions(Myself)
		ReallyForceSpell(Myself, WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself, WIZARD_STONE_SKIN)
		
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XA_IncubusHostile", "LOCALS", 1)
	Global("XA_PrisonKnowledge", "GLOBAL", 4)
	!Global("XA_IncubusBuff", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_IncubusBuff", "LOCALS", 1)
		ClearActions(Myself)
		ReallyForceSpell(Myself, WIZARD_GLOBE_OF_INVULNERABILITY)
		ReallyForceSpell(Myself, WIZARD_MIRROR_IMAGE)
		ReallyForceSpell(Myself, WIZARD_STONE_SKIN)
		ReallyForceSpell(Myself, WIZARD_SHIELD)
		
		SetInterrupt(TRUE)
		Continue()
END

IF
	HPPercentLT(Myself, 10)
	Global("XA_IncubusHostile", "LOCALS", 1)
THEN
	RESPONSE #33
		SetInterrupt(FALSE)
		DisplayStringHead(Myself, @88)
		CreateItem("XAQUIVER",0,0,0)
		DropItem("XAQUIVER",[-1.-1])
		SetGlobal("XA_IncubusDefeated", "GLOBAL", 1)
		SetGlobal("XA_IncubusQuiver", "GLOBAL", 1)
		Kill(Myself)		
		SetInterrupt(TRUE)
	RESPONSE #33
		SetInterrupt(FALSE)
		DisplayStringHead(Myself, @88)
		CreateItem("XASWORD",0,0,0)
		DropItem("XASWORD",[-1.-1])
		SetGlobal("XA_IncubusDefeated", "GLOBAL", 1)
		SetGlobal("XA_IncubusSword", "GLOBAL", 1)
		Kill(Myself)		
		SetInterrupt(TRUE)
	RESPONSE #33
		SetInterrupt(FALSE)
		DisplayStringHead(Myself, @88)
		CreateItem("XAMSRING",0,0,0)
		DropItem("XAMSRING",[-1.-1])
		SetGlobal("XA_IncubusDefeated", "GLOBAL", 1)
		SetGlobal("XA_IncubusRing", "GLOBAL", 1)
		Kill(Myself)		
		SetInterrupt(TRUE)
END

IF
	Global("XA_ConfrontDemon", "GLOBAL", 1)
	InMyArea(Player1)
THEN
	RESPONSE #100
		StartDialog("XAINCUB", Player1)
END

IF 
	InMyArea(Player1)
	Global("XA_DemonSpeak", "GLOBAL", 1)
	Global("XA_SanctumCS_End", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_DemonSpeak", "GLOBAL", 0)
		SetGlobal("XA_SanctumCS_End", "GLOBAL", 0)
		IncrementGlobal("XA_MetIncubus", "GLOBAL", 1)
		StartDialog("XAINCUB", Player1)
		SetInterrupt(TRUE)
END

IF
	HPPercentLT(Myself, 100)
	!Global("XA_ParleyAttack", "GLOBAL", 1)
	InMyArea(Player1)
	GlobalLT("XA_ConfrontDemon", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_ParleyAttack", "GLOBAL", 1)
		SetGlobal("XA_HealDemon", "GLOBAL", 1)
		StartDialog("XAINCUB", Player1)
		SetInterrupt(TRUE)
END

IF 
	!Global("XA_IncubusHostile", "LOCALS", 1)
THEN
	RESPONSE #100
		NoAction()
END

//fight AI

IF
	See(NearestEnemyOfType([0.0.0.0.0.SUMMONED]))
	Range(LastSeenBy(Myself),15)
	LevelLT(LastSeenBy(Myself),10)
	!GlobalTimerNotExpired("XA_IncubusDeath","LOCALS")
	RandomNum(2,1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_IncubusDeath","LOCALS",TWO_ROUNDS)
		ForceSpell(LastSeenBy(Myself),WIZARD_DEATH_SPELL)  // SPWI605.SPL (Death Spell)
		SetInterrupt(TRUE)
END

IF
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),10)
	CheckStat(Myself,0,STONESKINS)
	!GlobalTimerNotExpired("XA_IncubusStone","LOCALS")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_IncubusStone","LOCALS",TWO_MINUTES)
		ForceSpell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
		SetInterrupt(TRUE)
END

IF
	See(NearestEnemyOf(Myself))
	RandomNum(2,1)
	!StateCheck(NearestEnemyOf(Myself),STATE_CHARMED)
	!StateCheck(NearestEnemyOf(Myself),STATE_CONFUSED)
	!GlobalTimerNotExpired("XA_Gaze","LOCALS")
THEN
	RESPONSE #50
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Gaze","LOCALS",TWO_ROUNDS)
		FaceObject(NearestEnemyOf(Myself))
		ReallyForceSpellRES("beguile",NearestEnemyOf(Myself))
		SetInterrupt(TRUE)
	RESPONSE #50
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Gaze","LOCALS",TWO_ROUNDS)
		FaceObject(NearestEnemyOf(Myself))
		ReallyForceSpellRES("insanity",NearestEnemyOf(Myself))
		SetInterrupt(TRUE)		
		
END

IF
	!GlobalTimerNotExpired("XA_Finger","LOCALS")
	RandomNum(3,1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Finger","LOCALS",THREE_MINUTES)
		ForceSpell(NearestEnemyOf(Myself),CLERIC_FINGER_OF_DEATH)
		SetInterrupt(TRUE)	
END

IF
	!GlobalTimerNotExpired("XA_Unholy","LOCALS")
	RandomNum(2,1)
	See(NearestEnemyOf(Myself))
	!Alignment(NearestEnemyOf(Myself),MASK_EVIL)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Unholy","LOCALS",FIVE_ROUNDS)
		ForceSpell(Myself,CLERIC_UNHOLY_WORD)  // SPPR715.SPL (Unholy Word)
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("XA_Quake","LOCALS")
	RandomNum(3,1)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Quake","LOCALS",TEN_ROUNDS)
		ForceSpell(Myself,CLERIC_EARTHQUAKE)  // SPPR720.SPL (Earthquake)
		SetInterrupt(TRUE)
END

IF
	GlobalTimerNotExpired("XA_Harm","LOCALS")
	RandomNum(3,1)
	OR(2)
		See(NearestEnemyOf(Myself))
		See(LeastDamagedOf(Myself))
	Range(LastSeenBy(Myself),10)
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_Harm","LOCALS",FOUR_MINUTES)
		ForceSpell(LastSeenBy(Myself),ENEMY_CLERIC_HARM)
		SetInterrupt(TRUE)		
END

IF
	Range(NearestEnemyOf(Myself),4)
THEN
	RESPONSE #100
		RunAwayFrom(NearestEnemyOf(Myself),30)
	RESPONSE #50
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

IF
	Detect(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		AttackReevaluate(NearestEnemyOf(Myself),30)
END

