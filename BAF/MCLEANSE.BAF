//Modifications to BCOFFIN.bcs
//Daniel Valle
//dan@danielvalle.net

/* Revision History

2022-11-26: Initial Commit

*/


IF
	OR(5)
		Contains("MISCBL",Myself)  // Aerie's Body
		Contains("MISCBM",Myself)  // Anomen's Body
		Contains("MISCBN",Myself)  // Jaheira's Body
		Contains("MISCBO",Myself)  // Viconia's Body
		Contains("LCCORBOD",Myself) // Corwin's Body
	Global("CleanseVampire","AR1401",0)  // Shadow Temple
THEN
	RESPONSE #100
		SetGlobal("CleanseVampire","AR1401",1)  // Shadow Temple
END

/* Resurrect Corwin at the Shadow Temple */

IF
	Contains("LCCORBOD",Myself)  // Corwin's Body
	Contains("MISCBP",Myself)  // Bodhi's Black Heart
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("CleanseVampire","AR1401",2)  // Shadow Temple
		SetGlobal("LCA_CorwinCleansed", "GLOBAL", 1)
		DestroyItem("LCCORBOD")  // Corwin's Body
		DestroyItem("MISCBP")  // Bodhi's Black Heart
		CreateVisualEffect("SPMETSW2",[2592.1701])
		CreateVisualEffect("SPMETSW2",[2645.1742])
		Wait(1)
		CreateVisualEffect("SPFLESHS",[2565.1677])
		Wait(1)
		MoveGlobal("AR1401","LCCORWIN",[2565.1677])
		ApplySpell("LCCORWIN", CLERIC_CURE_DISEASE)
		ActionOverride(Player1, FaceObject("LCCORWIN"))
		Wait(1)
		ActionOverride("LCCORWIN", FaceObject(Player1))
		SmallWait(1)
		SetInterrupt(TRUE)
END