IF
	InMyArea(Player1)
	!Global("LCA_GorionDialogInit", "GLOBAL", 1)
THEN
	RESPONSE #100
		Wait(2)
		SetGlobal("LCA_GorionDialogInit", "GLOBAL", 1)
		ActionOverride("LCGORION", StartDialog("LCGORION", Player1))
END

IF
	Global("LCA_GorionHostile", "GLOBAL", 1)
	HPPercentLT("LCGORION", 100)
THEN
	RESPONSE #100
		SetGlobal("LCA_GorionHostile", "GLOBAL", 2)
		ApplySpell("LCGORION", WIZARD_SHIELD)
		ApplySpell("LCGORION", WIZARD_STONE_SKIN)
		ApplySpell("LCGORION", WIZARD_MIRROR_IMAGE)
		ApplySpell("LCGORION", WIZARD_GLOBE_OF_INVULNERABILITY)
		ActionOverride("LCGORION", Enemy())
END

IF 
	Dead("LCGORION")
	GlobalLT("LCA_EndG3", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_EndG3", "GLOBAL", 1)
		Continue()
END

IF
	Global("LCA_EndG3", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("LCA_EndG3", "GLOBAL", 2)
		SetGlobal("LCA_CompletedG3", "GLOBAL", 1)
		DisplayStringHead(Player1, ~(The world swirls, and your vision darkens...)~)
		Wait(3)
		SetGlobal("LCA_DemonSpeak", "GLOBAL", 1)
		SetInterrupt(TRUE)
		StartCutSceneMode()
		StartCutScene("LCSENT")
END

