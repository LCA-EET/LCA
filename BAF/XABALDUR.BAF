IF
	GlobalGT("L#2SkieDagger","GLOBAL",30)
	GlobalLT("XA_PromptToSendLetter", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_SkieModActive", "GLOBAL",1)
		SetGlobal("XA_PromptToSendLetter", "GLOBAL", 1)
		////DisplayStringHead(Player1, @1)
		ActionOverride(Player1, StartDialog("PLAYER1", Player1))
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XAAssist","GLOBAL",2)
	!TriggerOverride(Player1,HaveSpellRES("XAASSIS1")) 
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		ActionOverride(Player1, AddSpecialAbility("XAASSIS1"))
		SetGlobal("XAAssist","GLOBAL",0)
		////DisplayStringHead(Player1, @2)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("XAAssist","GLOBAL",2)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XAAssist","GLOBAL",0)
		//DisplayStringHead(Player1, @3)
		SetInterrupt(TRUE)
		Continue()
END

//{ Return to Baldur's Gate Timers

	IF
		Global("XA_ReturnToBG", "GLOBAL", 1)
		OR(6)
			AreaCheck("XAPAINT1")
			AreaCheck("XAG1")
			AreaCheck("XAG2A")
			AreaCheck("XAG3")
			AreaCheck("XADEMSAN")
			AreaCheck("XACSHOW")
		GlobalLT("XA_HaltRBGTimers", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_HaltRBGTimers", "GLOBAL", 1)
			//DisplayStringHead(Player1, @4)
	END
	
	IF
		Global("XA_ReturnToBG", "GLOBAL", 1)
		!AreaCheck("XAPAINT1")
		!AreaCheck("XAG1")
		!AreaCheck("XAG2A")
		!AreaCheck("XAG3")
		!AreaCheck("XADEMSAN")
		!AreaCheck("XACSHOW")
		Global("XA_HaltRBGTimers", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_HaltRBGTimers", "GLOBAL", 0)
			//DisplayStringHead(Player1, @5)
	END
	
	IF 
		Global("XA_ReturnToBG", "GLOBAL", 1)
		GlobalTimerExpired("XA_Advance7Timer", "GLOBAL")
		Global("XA_Advance7", "GLOBAL", 1)
	THEN
		RESPONSE #100
			SetGlobal("XA_Advance7", "GLOBAL", 0)
			//DisplayStringHead(Player1, @6)
			Continue()
	END
	//{ Leave for Family Timer
	
		//{ Set the Timer
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_CorwinLeaveForFamilyTimerSet", "GLOBAL", 1)
			Global("XA_CorwinMetFamily", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobalTimer("XA_CorwinLeaveForFamilyTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_CorwinLeaveForFamilyTimerSet", "GLOBAL", 1)
				//DisplayStringHead(Player1, @7)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
		//{ Advance the Timer by One Hour
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalTimerExpired("XA_CorwinLeaveForFamilyTimer", "GLOBAL")
			GlobalLT("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalLT("XA_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_CorwinLeaveForFamily_Hours", "GLOBAL", 1) // TICK
				SetGlobal("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
				SetInterrupt(TRUE)
				// Don't continue on the tick.
				//DisplayStringHead(Player1, @8)
		END
		//}
		
		//{ Have at least Four Hours Elapsed?
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			Global("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalGT("XA_CorwinLeaveForFamily_Hours", "GLOBAL", 3)
			GlobalLT("XA_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 2)
				SetGlobal("XA_CorwinLeaveForFamily", "GLOBAL", 1)
				//DisplayStringHead(Player1, @9)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
		//{ If not, then reset the timer.
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			Global("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 1)
			GlobalLT("XA_CorwinLeaveForFamily_Hours", "GLOBAL", 4)
			GlobalLT("XA_CorwinMetFamily", "GLOBAL", 2)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
			//If the timer has ticked, but has not reached the maximum alloted time, reset the timer.
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_CorwinLeaveForFamilyTicked", "GLOBAL", 0)
				SetGlobalTimer("XA_CorwinLeaveForFamilyTimer", "GLOBAL", ONE_HOUR)
				//DisplayStringHead(Player1, @10)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
	
	//}	
	
	//{ See Family Timer
	
		//{ Set the Timer
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_CorwinFamilyTimerSet", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobalTimer("XA_CorwinFamilyTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_CorwinFamilyTimerSet", "GLOBAL", 1)
				//DisplayStringHead(Player1, @11)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
		
		//{ Advance the Timer by One Hour
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalTimerExpired("XA_CorwinFamilyTimer", "GLOBAL")
			GlobalLT("XA_CorwinFamilyTimerTicked", "GLOBAL", 1)
			GlobalLT("XA_CorwinMetFamily", "GLOBAL", 1)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_CorwinFamilyTimerTicked", "GLOBAL", 1)
				IncrementGlobal("XA_CorwinFamilyTimer_Hours", "GLOBAL", 1)
				SetInterrupt(TRUE)
				//Don't continue on the tick
				//DisplayStringHead(Player1, @12)
		END
		//}
		
		//{ Corwin Complain about not seeing family.
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			Global("XA_CorwinFamilyTimerTicked", "GLOBAL", 1)
			GlobalLT("XA_CorwinMetFamily", "GLOBAL", 1)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			GlobalLT("XA_CorwinNeedSeeFamily", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetGlobal("XA_CorwinFamilyTimerTicked", "GLOBAL", 0)
				SetGlobal("XA_CorwinNeedSeeFamily", "GLOBAL", 1)
				//DisplayStringHead(Player1, @13)
		END
		//}

	//{ Duke Debriefed Timer
	
		//{ Set the Timer
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_DukeDebriefTimerSet", "GLOBAL", 1)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				SetGlobal("XA_DukeDebriefTimerSet", "GLOBAL", 1)
				SetGlobalTimer("XA_DukeDebriefTimer", "GLOBAL", ONE_HOUR)
				//DisplayStringHead(Player1, @14)
				SetInterrupt(TRUE)
				Continue()
		END
		//}
		
		//{ Advance the timer by One Hour
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			GlobalLT("XA_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalTimerExpired("XA_DukeDebriefTimer", "GLOBAL")
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetInterrupt(FALSE)
				IncrementGlobal("XA_DukeDebriefTimer_Hours", "GLOBAL", 1) // TICK
				SetGlobal("XA_Advance7", "GLOBAL", 0)
				SetGlobal("XA_DukeDebriefTimerTick", "GLOBAL", 1)
				SetInterrupt(TRUE)
				//DisplayStringHead(Player1, @15)
		END
		//}
	
		//{ Have 24 Hours Elapsed?
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			Global("XA_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalGT("XA_DukeDebriefTimer_Hours", "GLOBAL", 23)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_CorwinMetFamily", "GLOBAL", 2)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetGlobal("XA_DukeDebriefTimerTick", "GLOBAL", 2)
				SetGlobal("XA_DukeDebrief_BenceSpawn", "GLOBAL", 1)
				SetGlobal("XA_DukeDebrief_TimerElapsed", "GLOBAL", 1)
				//DisplayStringHead(Player1, @16)
				Continue()
		END
		//}
	
		//{ Less than 24 Hours Has Elapsed
		IF
			Global("XA_ReturnToBG", "GLOBAL", 1)
			Global("XA_DukeDebriefTimerTick", "GLOBAL", 1)
			GlobalLT("XA_DukeDebriefTimer_Hours", "GLOBAL", 24)
			GlobalLT("XA_DukesDebriefed", "GLOBAL", 1)
			Global("XA_HaltRBGTimers", "GLOBAL", 0)
		THEN
			RESPONSE #100
				SetGlobalTimer("XA_DukeDebriefTimer", "GLOBAL", ONE_HOUR)
				SetGlobal("XA_DukeDebriefTimerTick", "GLOBAL", 0)
				//DisplayStringHead(Player1, @17)
				Continue()
		END
		//}
//}

//{ Golden Egg
IF
	PartyHasItem("XACHICK")
	GlobalLT("XA_GoldenHenTimerSet", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_GoldenHenTimerSet", "GLOBAL", 1)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		//DisplayStringHead(Player1, @18)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player1)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player1, 0,0,0)
		DisplayString(Myself, @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		DisplayStringHead(Player1, @0)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player2)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player2, 0,0,0)
		DisplayStringHead(Player2, @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player3)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player3, 0,0,0)
		DisplayStringHead(Player3, @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player4)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player4, 0,0,0)
		DisplayString(Player4, @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player5)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player5, 0,0,0)
		DisplayString(Player5,  @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		SetInterrupt(TRUE)
		Continue()
END

IF
	HasItem("XACHICK", Player6)
	GlobalTimerExpired("XA_GoldenHenTimer", "GLOBAL")
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAEGG", Player6, 0,0,0)
		DisplayString(Player6,  @0)
		SetGlobalTimer("XA_GoldenHenTimer", "GLOBAL", ONE_DAY)
		SetInterrupt(TRUE)
		Continue()
END
//}

//{ Explicit Mod Check
IF 
	GlobalTimerExpired("XA_ExplicitModCheckTimer", "GLOBAL")
	Global("XA_ExplicitModCheckTimerSet", "GLOBAL", 1)
	Global("XA_ExplicitModActive","GLOBAL",1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_ExplicitModCheckTimer", "GLOBAL", ONE_HOUR)
		SetGlobal("XA_ExplicitModActive", "GLOBAL", 0)
		SetGlobal("XA_ExplicitModCheckTimerSet", "GLOBAL", 0)
		SetInterrupt(TRUE)
		//DisplayStringHead(Player1, @19)
END

//}

//{ Launch LCA Assistant
IF
	Global("XAAssist", "GLOBAL", 1)
THEN
	RESPONSE #100
		ActionOverride(Player1,StartDialog("player1",Player1))
END
//}

//{ Brega Timer Expiration
IF 
	GlobalTimerExpired("XA_Ath","GLOBAL")
	GlobalLT("XA_CheckWithBrega", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_CheckWithBrega", "GLOBAL", 1)
		ActionOverride(Player1,StartDialog("player1",Player1))
END

IF 
	GlobalTimerExpired("XA_Ath2","GLOBAL")
	GlobalLT("XA_CheckWithBrega2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_CheckWithBrega2", "GLOBAL", 1)
		ActionOverride(Player1,StartDialog("player1",Player1))
END
//}

//{ Wedding Hostilities
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("OHD_WEDDING_HOSTILE","OH5000",3)
	GlobalLT("XA_WeddingHostile", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_WeddingHostile", "GLOBAL", 1)
		Continue()
END
//}

//{ Defeated Irenicus in Spellhold
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("OpenJonBedroom","GLOBAL",1)
	GlobalLT("XA_PlayerSoulStolen", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_PlayerSoulStolen", "GLOBAL", 1)
		Continue()
END
//}

//{ Dorn Sex
IF	
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("OHD_sexreactions","GLOBAL",1)
	GlobalLT("XA_DornSex", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_DornSex", "GLOBAL", 1)
		Continue()
END
//}

//{ Bodhi was killed
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("BodhiDead","GLOBAL",1)
	GlobalLT("XA_BodhiDead", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_BodhiDead", "GLOBAL", 1)
		Continue()
END
//}

//{ Keldorn Reconciled with Maria
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("LadyMaria","GLOBAL",5) /* Keldorn reconciled with his wife */
	GlobalLT("XA_KeldornMaria", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_KeldornMaria", "GLOBAL", 1)
		Continue()
END
//}

//{ Phaere was alone with the player
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	GlobalGT("PhaereInnuendo","GLOBAL",1)
	GlobalLT("XA_AloneWithPhaere", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_AloneWithPhaere", "GLOBAL", 1)
		Continue()
END
//}

//{ Agreed to work for Bodhi
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("BodhiJob","GLOBAL",2)
	GlobalLT("XA_BodhiDeal2", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_BodhiDeal2", "GLOBAL", 1)
		Continue()
END

IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("BodhiJob","GLOBAL",3)
	GlobalLT("XA_BodhiDeal3", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_BodhiDeal3", "GLOBAL", 1)
		Continue()
END

IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("BodhiJob","GLOBAL",4)
	GlobalLT("XA_BodhiDeal4", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_BodhiDeal4", "GLOBAL", 1)
		Continue()
END
//}

//{ Agreed to Firkraag's Deal
IF
	ActionListEmpty()
	!InParty("XACORWIN")
	Global("FirkraagDeal","GLOBAL",1)
	GlobalLT("XA_FirkraagDeal", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_FirkraagDeal", "GLOBAL", 1)
		Continue()
END
//}

IF
	Dead("gromnir")  // Gromnir Il-Khan
	Global("DeathOfGromnir","GLOBAL",0)
	CombatCounter(0)
	!AreaCheck("AR4500")  // Pocket Plane
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		SetGlobal("DeathOfGromnir","GLOBAL",1)
		CreateCreatureObjectEffect("MEL01","SPFLESHS",Player1)  // Melissan
		Wait(1)
		PlaySong(79)
		ActionOverride("mel01",StartDialogueNoSet(Player1))
		EndCutSceneMode()
END
