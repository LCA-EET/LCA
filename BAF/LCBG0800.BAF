IF
	Global("LCA_ReturnToBG", "GLOBAL", 1)
	!Global("LCA_CreateElfsong", "GLOBAL", 1)
	!Dead("alyth")
	!Global("LCA_SavedElfsong", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_CreateElfsong", "GLOBAL", 1)
		CreateCreature("LC800M", [3150.1260], S)
		CreateCreature("LC800W", [3100.1260], S)
		SetInterrupt(FALSE)
		DisplayStringHead("LC800W", ~You hear? Another one's gone missing over at Elfsong.~)
		Wait(3)
		DisplayStringHead("LC800M", ~Aye. Poor Alyth must be pullin' her hair out tryin' to figure what's going on.~)
		SetInterrupt(TRUE)
		Continue()
END

IF 
	InMyArea("LC800M")
	InMyArea("LC800W")
	!Global("LCA_ElfsongTimerSet", "LOCALS", 1)
	!Global("LCA_SavedElfsong", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("LCA_ElfsongTimerSet", "LOCALS", 1)
		SetGlobalTimer("LCA_ElfsongTimer", "GLOBAL", TWO_MINUTES) 
END

IF 
	InMyArea("LC800M")
	InMyArea("LC800W")
	GlobalTimerExpired("LCA_ElfsongTimer", "GLOBAL")
	!Global("LCA_SavedElfsong", "GLOBAL", 1)	
	!Global("LCA_800MW", "GLOBAL", 1)
THEN
	RESPONSE #50
		SetGlobalTimer("LCA_ElfsongTimer", "GLOBAL", TWO_MINUTES) 
	RESPONSE #50
		SetInterrupt(FALSE)
		SetGlobalTimer("LCA_ElfsongTimer", "GLOBAL", TWO_MINUTES) 
		DisplayStringHead("LC800W", ~You hear? Another one's gone missing over at Elfsong.~)
		Wait(3)
		DisplayStringHead("LC800M", ~Aye. Poor Alyth must be pullin' her hair out tryin' to figure what's going on.~)
		SetInterrupt(TRUE)
END

IF
	Global("LCA_ReturnToBG", "GLOBAL", 1)
	!Global("LCA_NPCDestroy", "GLOBAL", 1)
THEN
	RESPONSE #100
		ActionOverride("NIKLOS", DestroySelf())
		ActionOverride("ELMIN5", DestroySelf())
		SetGlobal("LCA_NPCDestroy", "GLOBAL", 1)
		Continue()
END

IF 
	Global("LCA_ReturnToBG", "GLOBAL", 1)
	!Global("LCA_CreateMickNPC", "GLOBAL", 1)
	!InMyArea("MICK")
THEN
	RESPONSE #100
		CreateCreature("MICK", [510.2938], N)
		SetGlobal("LCA_CreateMickNPC", "GLOBAL", 1)
		Continue()
END

IF 
	Global("LCA_ReturnToBG", "GLOBAL", 1)
	!Global("LCA_CreateNeomaNPC", "GLOBAL", 1)
THEN
	RESPONSE #100
		CreateCreature("LCNEOMA", [3075.1390], N)
		SetGlobal("LCA_CreateNeomaNPC", "GLOBAL", 1)
		ActionOverride("LCNEOMA", RandomWalkContinuous())
END