//{ Amulet
IF
	GlobalLT("XA_CorwinAmulet", "LOCALS", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		GiveItemCreate("XAAMUL1", Myself, 0,0,0)
		XEquipItem("XAAMUL1","Corwin",SLOT_AMULET,EQUIP)
		SetGlobal("XA_CorwinAmulet", "LOCALS", 1)
		SetInterrupt(TRUE)
		Continue()
END
//}

//{ Corwin Opinion Baseline
IF
	GlobalLT("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 100)
		SetGlobal("XA_CorwinOpinionBaselined", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END
//}

//{ Post Ashatiel Duel
IF
	Dead("bdashati")
	Global("bd_asha_duel", "GLOBAL", 3)
	IsValidForPartyDialog(Player1)
	Global("XA_CorwinWitnessedDuel", "GLOBAL", 1)
	!ActuallyInCombat()
THEN
	RESPONSE #100
		StartDialog("BDCORWIJ", Player1)
END
//}


//{ Opinion Boosts
IF
	Dead("BDLON")
	GlobalLT("XA_LCA_BDLON", "GLOBAL", 1)
	Global("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_BDLON", "GLOBAL", 1)
		IncrementGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("BD_Refugees","GLOBAL",2)
	GlobalLT("XA_LCA_GARACH", "GLOBAL", 1)
	Global("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_GARACH", "GLOBAL", 1)
		IncrementGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("BD_TO_THE_BANK","GLOBAL",2)
	GlobalLT("XA_LCA_TEERA", "GLOBAL", 1)
	Global("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_TEERA", "GLOBAL", 1)
		IncrementGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("bd_alerted_corwin", "GLOBAL", 2)
	GlobalLT("XA_LCA_PLOT", "GLOBAL", 1)
	Global("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_PLOT", "GLOBAL", 1)
		IncrementGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF
	Global("BD_Rousers_Reinforcements","GLOBAL",1)
	GlobalLT("XA_LCA_VIDA", "GLOBAL", 1)
	Global("XA_CorwinOpinionBaselined", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_VIDA", "GLOBAL", 1)
		IncrementGlobal("XA_CorwinOpinionOfPlayer", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END
//}

//{ Longbow Training

IF
	InParty(Myself)
	GlobalLT("XA_LCA_ProficiencyTimerSet", "GLOBAL", 1)
	GlobalGT("XA_CorwinOpinionOfPlayer", "GLOBAL", 102)
	OR(2)
		CheckStatGT(Player1, 0, PROFICIENCYLONGBOW)
		CheckStatGT(Player1, 0, PROFICIENCYSHORTBOW)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("XA_LCA_ProficiencyTimer", "GLOBAL", EIGHTEEN_HOURS)
		SetGlobal("XA_LCA_ProficiencyTimerSet", "GLOBAL", 1)
		SetInterrupt(TRUE)
		Continue()
END

IF
	ActionListEmpty()
	!ActuallyInCombat()
	!HasItemCategory(Player1, BOW, FALSE)
	InParty(Myself)
	See(Player1)
	IsValidForPartyDialog(Myself)
	IsValidForPartyDialog(Player1)
	GlobalTimerExpired("XA_LCA_ProficiencyTimer", "GLOBAL")
	TimeOfDay(DAY)
	GlobalLT("XA_LCA_ProficiencyTalk", "GLOBAL", 1)
	Range("PlayerChest00", 50)
	OR(2)
		IsWeaponRanged(Player1)
		TriggerOverride(Player1, CanEquipRanged())
	OR(2)
		AreaCheck("BD3000") //Allied Siege Camp
		AreaCheck("BD1000") //Coast Way Crossing
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobal("XA_LCA_ProficiencyTalk", "GLOBAL", 1)
		StartDialog("BDCORWIJ", Player1)
		SetInterrupt(TRUE)
END
//}