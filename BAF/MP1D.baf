IF
	GlobalTimerExpired("LCA_STtoBG","GLOBAL") // see MBREGA.d state M34
	!Global("LCA_CS03_Done", "GLOBAL", 1)
	OR(2)
	Global("LCA_LetterSent", "GLOBAL", 1) // Corwin Love
	Global("LCA_LetterSent", "GLOBAL", 2) // Corwin 
THEN
  RESPONSE #100
	SetGlobal("LCA_CS03_Done", "GLOBAL", 1)
    StartCutSceneMode()
    StartCutSceneEx("LCAcs03a",FALSE)
END

IF
	GlobalTimerExpired("LCA_STtoBG","GLOBAL") // see MBREGA.d state M34
	Global("LCA_LetterSent", "GLOBAL", 3) // Duncan
	!Global("LCA_CS02_Done", "GLOBAL", 1)
THEN
  RESPONSE #100
	SetGlobal("LCA_CS02_Done", "GLOBAL", 1)
    StartCutSceneMode()
    StartCutSceneEx("LCAcs02a",FALSE)
END

IF
  GlobalTimerExpired("LCA_CS04","GLOBAL")
  !Global("LCA_CS04_Done", "GLOBAL", 1)
THEN
  RESPONSE #100
	SetGlobal("LCA_CS04_Done", "GLOBAL", 1)
    StartCutSceneMode()
    StartCutSceneEx("LCAcs04a",FALSE)
END

IF
  GlobalTimerExpired("LCA_CS05","GLOBAL")
  !Global("LCA_CS05_Done", "GLOBAL", 1)
THEN
  RESPONSE #100
	SetGlobal("LCA_CS05_Done", "GLOBAL", 1)
    StartCutSceneMode()
    StartCutSceneEx("LCAcs05a",FALSE)
END

IF
  GlobalTimerExpired("LCA_CS01","GLOBAL")
  !Global("LCA_CS01_Done", "GLOBAL", 1)
THEN
  RESPONSE #100
	SetGlobal("LCA_CS01_Done", "GLOBAL", 1)
    StartCutSceneMode()
    StartCutSceneEx("LCAcs01a",FALSE)
END

IF 
	Global("LCA_CorwinSexFirst", "GLOBAL", 1)
	Global("LCA_CorwinCanHaveSex", "GLOBAL", 1)
	OR(13)
		AreaCheck("AR0021") //Crooked Crane 1FL
		AreaCheck("AR0022") //Crooked Crane 2FL
		AreaCheck("AR0313") //Sea Bounty 1FL
		AreaCheck("AR0314") //Sea Bounty 2FL
		AreaCheck("AR0509") //Five Flagons, 1FL
		AreaCheck("AR0511") //Five Flagons, 2FL
		AreaCheck("AR0513") //Inn at Bridge District, 1FL
		AreaCheck("AR0514") //Inn at Bridge District, 2FL
		AreaCheck("AR0515") //Inn at Bridge District, 3FL
		AreaCheck("AR0704") //Mithrest Inn
		AreaCheck("AR1105") //Imnesvale Inn
		AreaCheck("AR1602") //Brynnlaw's Inn
		AreaCheck("AR2010") //Trademeet's Inn
THEN
	RESPONSE #100
		SetGlobal("LCA_SleepInn", "GLOBAL", 1)
		StartCutSceneMode()
		StartCutSceneEx("LCAcs10a",FALSE)
END

IF 
	Global("LCA_CorwinSexSecond", "GLOBAL", 3)
	Global("LCA_CorwinCanHaveSex", "GLOBAL", 1)
	OR(13)
		AreaCheck("AR0021") //Crooked Crane 1FL
		AreaCheck("AR0022") //Crooked Crane 2FL
		AreaCheck("AR0313") //Sea Bounty 1FL
		AreaCheck("AR0314") //Sea Bounty 2FL
		AreaCheck("AR0509") //Five Flagons, 1FL
		AreaCheck("AR0511") //Five Flagons, 2FL
		AreaCheck("AR0513") //Inn at Bridge District, 1FL
		AreaCheck("AR0514") //Inn at Bridge District, 2FL
		AreaCheck("AR0515") //Inn at Bridge District, 3FL
		AreaCheck("AR0704") //Mithrest Inn
		AreaCheck("AR1105") //Imnesvale Inn
		AreaCheck("AR1602") //Brynnlaw's Inn
		AreaCheck("AR2010") //Trademeet's Inn
THEN
	RESPONSE #100
		SetGlobal("LCA_SleepInn", "GLOBAL", 1)
		StartCutSceneMode()
		StartCutSceneEx("LCAcs11a",FALSE)
END

IF 
	Global("LCA_CorwinSexFirst", "GLOBAL", 1)
	Global("LCA_CorwinCanHaveSex", "GLOBAL", 1)
	AreaCheck("AR0406") // Copper Coronet
THEN
	RESPONSE #100
		SetGlobal("LCA_SleepInn", "GLOBAL", 1)
		ActionOverride("LCCORWIN", StartDialog("LCCORWIJ", Player1))
END

IF 
	!Global("LCA_CorwinCanHaveSex", "GLOBAL", 1)
	OR(3)
		Global("LCA_CorwinSexFirst", "GLOBAL", 1)
		Global("LCA_CorwinSexBG", "GLOBAL", 1)
		Global("LCA_CorwinSexToB", "GLOBAL", 1)
THEN 
	RESPONSE #100
		SetGlobal("LCA_TryToSleep", "GLOBAL", 1)
		ActionOverride("LCCORWIN", StartDialog("LCCORWIJ", Player1))
END