IF
	AreaCheck("BG0706")
	Global("XA_SavedLyriel", "GLOBAL", 2)
	InMyArea(Player1)
THEN
	RESPONSE #100
		SetGlobal("XA_SavedLyriel", "GLOBAL", 3)
		StartDialog("XALYRIEL", Player1)
END
IF
	AreaCheck("XADEMSAN")
	Global("XA_DemonDead", "GLOBAL", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_DemonDead", "GLOBAL", 2)
		StartDialog("XALYRIEL", Player1)
END

IF
	Global("XA_DemonDead", "GLOBAL", 4)
THEN
	RESPONSE #100
		StartDialog("XALYRIEL", Player1)
END

IF
	AreaCheck("XAPAINT1")
	GlobalLT("XA_Lyriel", "GLOBAL", 1)
	!Global("XA_Sing", "LOCALS", 1)
THEN
	RESPONSE #100
		SetGlobal("XA_Sing", "LOCALS", 1)
		DisplayStringHead(Myself, ~(Soft, melancholy singing.)~)
		SetGlobalTimer("XA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
		Continue()
END

IF
	AreaCheck("XAPAINT1")
	GlobalLT("XA_Lyriel", "GLOBAL", 1)
	Range([ANYONE], 8)
THEN
	RESPONSE #100
		FaceObject(Player1)
		SmallWait(1)
		StartDialog("XALYRIEL", Player1)
END

IF
	AreaCheck("XAPAINT1")
	GlobalTimerExpired("XA_LyrielSingTimer", "GLOBAL")
	!Global("XA_LeavePrison", "GLOBAL", 1)
THEN
	RESPONSE #50
		DisplayStringHead(Myself, ~(Soft, melancholy singing.)~)
		SetGlobalTimer("XA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
	RESPONSE #50
		SetGlobalTimer("XA_LyrielSingTimer", "GLOBAL", TWO_ROUNDS)
END